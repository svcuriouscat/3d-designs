// ShapeScript document

/// Epoxy Cooling Tray â€” West System 16oz (805)
/// Meant to be filled with water and frozen to be used as an ice tray for your epoxy mixing pot
/// Lowering the temperature prevents epoxy from kicking off too soon

//////////////////////////////////////////////////////////////////////////////

detail 240
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

define PI                   3.14159265359
/// Pot
define POTRADIUSBOTTOM     94       /// mm
define POTRADIUSTOP       104.8     /// mm
define POTHEIGHT           64       /// mm
/// Tray
define TRAYDEPTH           (POTHEIGHT / 2)  /// mm
define TRAYTHICKNESS       (POTRADIUSBOTTOM / 10)  /// mm
define TRAYWALLTHICKNESS    1       /// mm
define TRAYTUNNELDIAMETER  (TRAYTHICKNESS * (PI / 2) - TRAYWALLTHICKNESS * 2)  /// mm
define TRAYTUNNELDISTANCE  (TRAYWALLTHICKNESS * 2)  /// mm (values less than 1.2 likely won't print well on most FDM printers)
define TRAYTUNNELCOUNT     (round(POTRADIUSBOTTOM / (TRAYTUNNELDIAMETER + TRAYTUNNELDISTANCE)) - 1)  /// count

//////////////////////////////////////////////////////////////////////////////

define POT {
    lathe path {
        point 0  POTHEIGHT
        point (POTRADIUSTOP / 2)  POTHEIGHT
        point (POTRADIUSBOTTOM / 2)  0
        point 0  0
    }
}

define TRAY {
    difference {
        /// Base
        difference {
            lathe path {
                point 0  POTHEIGHT
                point (POTRADIUSTOP / 2 + TRAYTHICKNESS)  POTHEIGHT
                point (POTRADIUSBOTTOM / 2 + TRAYTHICKNESS)  0
                point 0  0
            }

            POT {
                position 0  (TRAYTUNNELDIAMETER + TRAYWALLTHICKNESS * 2)
            }

            /// Cut off the top
            cube {
                size (POTRADIUSTOP + TRAYTHICKNESS * 2 + TRAYWALLTHICKNESS)  (POTHEIGHT - TRAYDEPTH)  (POTRADIUSTOP + TRAYTHICKNESS * 2 + TRAYWALLTHICKNESS)
                position 0  ((POTHEIGHT + TRAYTUNNELDIAMETER) - ((POTHEIGHT - TRAYDEPTH) / 2))
            }

            /// Put angle into the inner chamber to prevent final layers from sagging
            cone {
                orientation 1
                size (POTRADIUSTOP + TRAYTHICKNESS * 2 + TRAYWALLTHICKNESS * 2)  (TRAYTHICKNESS * 2)
                position 0  (POTHEIGHT / 2 + TRAYTHICKNESS / 2)
            }
        }

        /// Internal chamber for water (ice)
        difference {
            lathe path {
                point 0  (POTHEIGHT)
                point (POTRADIUSTOP / 2 + (TRAYTHICKNESS / 2) - TRAYWALLTHICKNESS)  (POTHEIGHT)
                point (POTRADIUSBOTTOM / 2 + (TRAYTHICKNESS / 2) - TRAYWALLTHICKNESS)  TRAYWALLTHICKNESS + (TRAYTUNNELDIAMETER / 2)
                point 0  TRAYWALLTHICKNESS + (TRAYTUNNELDIAMETER / 2)
            }

            difference {
                union {
                    lathe path {
                        point 0  (POTHEIGHT - TRAYWALLTHICKNESS)
                        point (POTRADIUSTOP / 2 + TRAYWALLTHICKNESS / 1.67)  (POTHEIGHT - TRAYWALLTHICKNESS)
                        point (POTRADIUSBOTTOM / 2 + TRAYWALLTHICKNESS / 1.67)  TRAYWALLTHICKNESS + (TRAYTUNNELDIAMETER / 2)
                        point 0  TRAYWALLTHICKNESS + (TRAYTUNNELDIAMETER / 2)
                    }

                    /// Put angle into the inner chamber to prevent final layers from sagging
                    cone {
                        orientation 1
                        size (POTRADIUSTOP + TRAYTHICKNESS * 2 + TRAYWALLTHICKNESS * 2)  (TRAYTHICKNESS * 2)
                        position 0  (POTHEIGHT / 2 + TRAYTHICKNESS / 2 - TRAYWALLTHICKNESS)
                    }

                    /// Extend base for tunnels
                    cylinder {
                        size (POTRADIUSBOTTOM + TRAYWALLTHICKNESS * 2)  (TRAYTUNNELDIAMETER)
                        position 0  (TRAYWALLTHICKNESS + TRAYTUNNELDIAMETER / 2)
                    }
                }

                /// Tunnels (supports)
                union {
                    for i in -floor(TRAYTUNNELCOUNT / 2) to floor(TRAYTUNNELCOUNT / 2) {
                        cylinder {
                            detail 40
                            orientation 0.5
                            size (TRAYTUNNELDIAMETER)  (POTRADIUSBOTTOM + TRAYTHICKNESS)
                            position 0  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)  ((TRAYTUNNELDIAMETER + TRAYTUNNELDISTANCE) * i)
                        }
                    }
                    for j in -floor(TRAYTUNNELCOUNT / 2) to floor(TRAYTUNNELCOUNT / 2) {
                        cylinder {
                            detail 40
                            orientation 0.5  0  0.5
                            size (TRAYTUNNELDIAMETER)  (POTRADIUSBOTTOM + TRAYTHICKNESS)
                            position ((TRAYTUNNELDIAMETER + TRAYTUNNELDISTANCE) * j)  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)
                        }
                    }

                    /// Shift a bit for symmetry
                    //position (TRAYTUNNELDISTANCE * 2)  0  (TRAYTUNNELDISTANCE * 2)
                }

                /// Chop off floating bits of ceiling lefts from tunnels
                group {
                    cube {
                        size TRAYTUNNELDIAMETER
                        position 35.91  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)  35.91
                    }
                    cube {
                        size TRAYTUNNELDIAMETER
                        position -35.91  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)  35.91
                    }
                    cube {
                        size TRAYTUNNELDIAMETER
                        position 35.91  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)  -35.91
                    }
                    cube {
                        size TRAYTUNNELDIAMETER
                        position -35.91  (TRAYTUNNELDIAMETER / 2 + TRAYWALLTHICKNESS)  -35.91
                    }
                }
            }

            /// Cut off the top
            cube {
                size (POTRADIUSTOP + TRAYTHICKNESS * 2)  (POTHEIGHT - TRAYDEPTH)  (POTRADIUSTOP + TRAYTHICKNESS * 2)
                position 0  ((POTHEIGHT + TRAYTUNNELDIAMETER) - ((POTHEIGHT - TRAYDEPTH) / 2) - TRAYWALLTHICKNESS)
            }
        }

        /// Little hole to fill in and drain
        cylinder {
            size 3  (TRAYWALLTHICKNESS * 7)
            orientation 0.5
            position 50  (TRAYDEPTH - (3 / 2) - TRAYWALLTHICKNESS * 4)
            size 0
        }
    }

    /// Alter tunnels' shape
    union {
        /// Make tunnels narrow towards the center to make the middle freeze first
        cone {
            color 1  0  0
            size (POTRADIUSBOTTOM + TRAYWALLTHICKNESS * 2)  (TRAYTUNNELDIAMETER / 2)
            position 0  (TRAYWALLTHICKNESS + TRAYTUNNELDIAMETER * 0.75)
        }
    }
}

//////////////////////////////////////////////////////////////////////////////

POT {
    color 1 0 0
    opacity 0.25
    position 0  TRAYTHICKNESS
    size 0
}

TRAY {
    opacity 0.25
}

//////////////////////////////////////////////////////////////////////////////

print "[TUNNEL] diameter: " TRAYTUNNELDIAMETER "mm"
print "[TUNNEL] distance: " TRAYTUNNELDISTANCE "mm"
print "[TUNNEL] count: " TRAYTUNNELCOUNT " x 2"
