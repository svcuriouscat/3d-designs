// ShapeScript document

/// Portlight Flyscreen - Lewmar Atlantic Size 32

detail 128
rotate 0  0  -0.5  /// CAD mode

/// Border
define BORDERHEIGHT         117     /// mm
define BORDERWIDTH          320     /// mm
define BORDERTHICKNESS        3     /// mm
/// Mounting tabs
define TABSHEIGHT           124     /// mm
define TABSWIDTH            327     /// mm
define TABSSIZEHORIZ         18     /// mm
define TABSSIZEVERT          16     /// mm
/// Grill
define GRILLLINETHICKNESS     0.5   /// mm
define GRILLHOLESIZE          2     /// mm
define GRILLLINECOUNTHORIZ    (BORDERHEIGHT - BORDERTHICKNESS * 2) / (GRILLLINETHICKNESS + GRILLHOLESIZE) / 1.4  /// count
define GRILLLINECOUNTVERT     (BORDERWIDTH - BORDERTHICKNESS * 2) / (GRILLLINETHICKNESS + GRILLHOLESIZE)  /// count

define BORDERSHAPE {
    union {
        cylinder {
            size BORDERHEIGHT  BORDERTHICKNESS  BORDERHEIGHT
            position (BORDERWIDTH / 2 - BORDERHEIGHT / 2)
        }

        cube {
            size (BORDERWIDTH - BORDERHEIGHT)  BORDERTHICKNESS  BORDERHEIGHT
        }

        cylinder {
            size BORDERHEIGHT  BORDERTHICKNESS  BORDERHEIGHT
            position -(BORDERWIDTH / 2 - BORDERHEIGHT / 2)
        }
    }
}

define CUTOUT {
    union {
        cylinder {
            size (BORDERHEIGHT - BORDERTHICKNESS * 2)  (BORDERTHICKNESS)  (BORDERHEIGHT - BORDERTHICKNESS * 2)
            position (BORDERWIDTH / 2 - BORDERHEIGHT / 2)
        }

        cube {
            size (BORDERWIDTH - BORDERHEIGHT)  (BORDERTHICKNESS)  (BORDERHEIGHT - BORDERTHICKNESS * 2)
        }

        cylinder {
            size (BORDERHEIGHT - BORDERTHICKNESS * 2)  (BORDERTHICKNESS)  (BORDERHEIGHT - BORDERTHICKNESS * 2)
            position -(BORDERWIDTH / 2 - BORDERHEIGHT / 2)
        }
    }
}

define BORDER {
    difference {
        BORDERSHAPE
        CUTOUT
    }
}

define TABSSHAPE {
    union {
        sphere {
            size TABSHEIGHT  BORDERTHICKNESS  TABSHEIGHT
            position (TABSWIDTH / 2 - TABSHEIGHT / 2)
        }

        cylinder {
            orientation  0.5
            size (BORDERTHICKNESS)  (TABSWIDTH - TABSHEIGHT)  TABSHEIGHT
        }

        sphere {
            size TABSHEIGHT  BORDERTHICKNESS  TABSHEIGHT
            position -(TABSWIDTH / 2 - TABSHEIGHT / 2)
        }
    }
}

define TABS {
    intersection {
        /// Base
        difference {
            TABSSHAPE {
                position 0  -(BORDERTHICKNESS / 2 - 0.5)
            }

            /// Cut out hole in the middle (for the border)
            BORDERSHAPE {
                size 1  10  1
            }
        }

        /// Levers
        union {
            /// Horizontal
            cylinder {
                size TABSSIZEHORIZ  BORDERTHICKNESS
                position (BORDERWIDTH / 2 - TABSSIZEHORIZ / 10)
            }
            cylinder {
                size TABSSIZEHORIZ  BORDERTHICKNESS
                position -(BORDERWIDTH / 2 - TABSSIZEHORIZ / 10)
            }
            /// Vertical
            group {
                cylinder {
                    size TABSSIZEVERT  BORDERTHICKNESS
                    position (BORDERWIDTH / 4)  0  -(BORDERHEIGHT / 2 - TABSSIZEVERT / 10)
                }
                cylinder {
                    size TABSSIZEVERT  BORDERTHICKNESS
                    position -(BORDERWIDTH / 4)  0  -(BORDERHEIGHT / 2 - TABSSIZEVERT / 10)
                }
                cylinder {
                    size TABSSIZEVERT  BORDERTHICKNESS
                    position (BORDERWIDTH / 4)  0  (BORDERHEIGHT / 2 - TABSSIZEVERT / 10)
                }
                cylinder {
                    size TABSSIZEVERT  BORDERTHICKNESS
                    position -(BORDERWIDTH / 4)  0  (BORDERHEIGHT / 2 - TABSSIZEVERT / 10)
                }
            }
        }
    }
}

define GRILL {
    intersection {
        CUTOUT

        union {
            group {
                /// Horizontal lines (angled)
                for i in -(GRILLLINECOUNTHORIZ / 2) to (GRILLLINECOUNTHORIZ / 2) {
                    cube {
                        size (BORDERWIDTH)  (BORDERHEIGHT)  (GRILLLINETHICKNESS)
                        position 0  0  (i * (GRILLLINETHICKNESS + GRILLHOLESIZE))
                    }
                }

                orientation 0  0  -0.25
                /// Move down a little to have as many round holes as possible
                position 0  -(GRILLHOLESIZE / 2 + GRILLLINETHICKNESS / 2)
            }

            group {
                /// Vertical lines
                for i in -(GRILLLINECOUNTVERT / 2) to (GRILLLINECOUNTVERT / 2) {
                    cube {
                        size (GRILLLINETHICKNESS)  (BORDERTHICKNESS)  (BORDERHEIGHT - BORDERTHICKNESS * 2)
                        position (i * (GRILLLINETHICKNESS + GRILLHOLESIZE) - ((GRILLLINETHICKNESS + GRILLHOLESIZE) / 4))
                    }
                }

                /// Move them a little to make the grill symmetrical
                position (GRILLHOLESIZE / 2 + GRILLLINETHICKNESS / 2)-(GRILLHOLESIZE / 2 + GRILLLINETHICKNESS / 2)
            }
        }
    }
}

define PORTLIGHTFLYSCREEN {
    union {
        TABS
        BORDER
        GRILL
    }
}

PORTLIGHTFLYSCREEN {
    opacity 0.1
}
