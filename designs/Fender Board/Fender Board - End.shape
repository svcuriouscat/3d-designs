// ShapeScript document

/// Fender Board â€” End

detail 100
rotate 0  0  -0.5  /// CAD mode

define INCH                   25.4        /// mm
define FENDERBOARDTHICKNESS   1 * INCH    /// mm
define FENDERBOARDHEIGHT      6 * INCH    /// mm
define LINETHICKNESS          0.5 * INCH  /// mm

define HOLE {
    union {
        cylinder {
            size LINETHICKNESS  (FENDERBOARDHEIGHT * 2)
            orientation 0  0  0.5
        }

        /// Top
        difference {
            cylinder {
                size FENDERBOARDTHICKNESS  FENDERBOARDTHICKNESS / 4
                position 0  0  -(FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2 / 2 / 2)
                orientation 0  0  0.5
            }

            intersection {
                cylinder {
                    size FENDERBOARDTHICKNESS  (FENDERBOARDHEIGHT / 2)
                    orientation 0.5
                    position 0  0  -(FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
                }

                extrude {
                    circle {
                        size (FENDERBOARDTHICKNESS - LINETHICKNESS)
                    }
                    along circle {
                        size (FENDERBOARDTHICKNESS)
                        position 0  0  -(FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2 / 2)
                    }
                }
            }
        }

        /// Bottom
        difference {
            cylinder {
                size FENDERBOARDTHICKNESS  FENDERBOARDTHICKNESS / 4
                position 0  0  (FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2 / 2 / 2)
                orientation 0  0  0.5
            }

            intersection {
                cylinder {
                    size FENDERBOARDTHICKNESS  (FENDERBOARDHEIGHT / 2)
                    orientation 0.5
                    position 0  0  (FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
                }

                extrude {
                    circle {
                        size (FENDERBOARDTHICKNESS - LINETHICKNESS)
                    }
                    along circle {
                        size (FENDERBOARDTHICKNESS)
                        position 0  0  (FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2 / 2)
                    }
                }
            }
        }
    }
}

define FENDERBOARDEND {
    difference {
        /// Body
        union {
            cube {
                size (FENDERBOARDHEIGHT / 2)  (FENDERBOARDTHICKNESS)  (FENDERBOARDHEIGHT - FENDERBOARDTHICKNESS)
            }
            cylinder {
                size FENDERBOARDTHICKNESS  (FENDERBOARDHEIGHT / 2)
                orientation 0.5
                position 0  0  -(FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
            }
            cylinder {
                size FENDERBOARDTHICKNESS  (FENDERBOARDHEIGHT / 2)
                orientation 0.5
                position 0  0  (FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
            }
            cylinder {
                size FENDERBOARDHEIGHT - FENDERBOARDTHICKNESS  FENDERBOARDTHICKNESS
                position -((FENDERBOARDHEIGHT) / 4)
            }
            extrude {
                circle {
                    size FENDERBOARDTHICKNESS
                }
                along circle {
                    size FENDERBOARDHEIGHT - FENDERBOARDTHICKNESS
                    orientation 0  0  0.5
                    position -((FENDERBOARDHEIGHT) / 4)
                }
            }
        }

        /// Holes
        HOLE {
            position (FENDERBOARDHEIGHT / 8)
        }
        HOLE {
            position -(FENDERBOARDHEIGHT / 4)
        }

        /// Core dimples
        union {
            cylinder {
                size FENDERBOARDTHICKNESS  (FENDERBOARDHEIGHT - FENDERBOARDTHICKNESS)
                orientation 0  0  0.5
            }

            /// Top
            sphere {
                size FENDERBOARDTHICKNESS
                position 0  0  -(FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
            }

            /// Bottom
            sphere {
                size FENDERBOARDTHICKNESS
                position 0  0  (FENDERBOARDHEIGHT / 2 - FENDERBOARDTHICKNESS / 2)
            }

            position (FENDERBOARDHEIGHT / 4 + 0.8)
        }
    }
}

FENDERBOARDEND {
    color 150  150  150
    opacity 0.5
}
