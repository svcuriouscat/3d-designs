// ShapeScript document

// Rudder Quadrant Bumper - Starboard 2

detail 128
rotate 0 0 -0.5 // CAD

define PI                           3.14159265359
define QUADRANTDIAMETER           400    // mm
define QUADRANTCIRCUMFERENCE QUADRANTDIAMETER*3.14
define QUADRANTSTOPHEIGHT          30    // mm
define QUADRANTSTOPWIDTH           75    // mm
define QUADRANTSTOPOUTERLENGTH    110    // mm
define SCREWDIAMETER                8    // mm

define FULLCIRCLE {
    difference {
        cylinder {
            size QUADRANTDIAMETER QUADRANTSTOPHEIGHT
        }

        cylinder {
            size 400-QUADRANTSTOPWIDTH*2 QUADRANTSTOPHEIGHT
        }
    }
}

define YOUWILLNEVERBEAFULLCIRCLE {
    cube {
        size QUADRANTDIAMETER  QUADRANTSTOPHEIGHT  QUADRANTDIAMETER/2
        position  0 0 -QUADRANTDIAMETER/4
    }
}

define CURVATURE {
    cylinder {
        size QUADRANTSTOPWIDTH  QUADRANTSTOPHEIGHT
        position 0 0 QUADRANTDIAMETER/2-QUADRANTSTOPWIDTH/2
    }
}

define SCREWHOLE {
    cylinder {
        size SCREWDIAMETER QUADRANTSTOPHEIGHT
        position 0 0 QUADRANTDIAMETER/2-QUADRANTSTOPWIDTH/2
    }
}

define BUMPER {
    difference {
        union {
            difference {
                FULLCIRCLE

                YOUWILLNEVERBEAFULLCIRCLE {
                    orientation 0 (0.5-(QUADRANTSTOPOUTERLENGTH/QUADRANTCIRCUMFERENCE))
                }
                YOUWILLNEVERBEAFULLCIRCLE {
                    orientation 0 -(0.5-(QUADRANTSTOPOUTERLENGTH/QUADRANTCIRCUMFERENCE))
                }
            }

            CURVATURE {
                orientation 0 0.04
            }
        }

        /// Screwholes
        SCREWHOLE {
            orientation 0 0+0.05
            position 10 0 -15
        }
        SCREWHOLE {
            orientation 0 0-0.05
            position 5 0 12
        }
    }
}

BUMPER