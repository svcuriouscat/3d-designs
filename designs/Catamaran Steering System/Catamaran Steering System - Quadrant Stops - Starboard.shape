// ShapeScript document

/// Catamaran Steering System - Quadrant Stops - Starboard

detail 255
rotate 0  0  -0.5  /// CAD mode

define PI                         3.14159265359
/// Rudder
define RUDDERTURNANGLE           75    /// degrees
/// Quadrant
define QUADRANTDIAMETER         400    /// mm
define QUADRANTCIRCUMFERENCE    QUADRANTDIAMETER * PI  /// mm
/// Fasteners
define SCREWDIAMETER              9.45  /// mm (3/8")
/// Stops
define STOPHEIGHT                35    /// mm
define STOPWIDTH                 80    /// mm
define STOP1RADIALLENGTH        125    /// mm (radial length of first stop)
define STOP2RADIALLENGTH        125    /// mm (radial length of first stop)
define STOPSRADIALDISTANCE      QUADRANTCIRCUMFERENCE * (RUDDERTURNANGLE / 360)  /// mm (radial distance between two stops)
/// Middle stopper
define MIDDLESTOPPERTHICKNESS     8    /// mm (thickness of aluminum L-bracket attached to the hull)

define SCAN {
    import "scans/quadrant-stbd.stl"
}

define FULLCIRCLE {
    difference {
        cylinder {
            size QUADRANTDIAMETER  STOPHEIGHT
        }

        cylinder {
            size (QUADRANTDIAMETER - STOPWIDTH * 2)  STOPHEIGHT
        }
    }
}

define YOUWILLNEVERBEAFULLCIRCLE {
    cube {
        size (QUADRANTDIAMETER)  (STOPHEIGHT)  (QUADRANTDIAMETER / 2)
        position 0  0  -(QUADRANTDIAMETER / 4)
    }
}

define SCREWHOLE {
    cylinder {
        color 100  0  0
        size (SCREWDIAMETER + 0.6)  (STOPHEIGHT)
        position 0  0  (QUADRANTDIAMETER / 2)
    }
}

define NUTHOLE {
    cylinder {
        color 100  0  0
        size 25.4  (STOPHEIGHT)
        position 0  0  (QUADRANTDIAMETER / 2)
    }
}

define CURVATURE {
    cylinder {
        size (STOPWIDTH)  STOPHEIGHT
        position (QUADRANTDIAMETER / 2 - STOPWIDTH / 2)
    }
}

define STOP1 {
    union {
        difference {
            FULLCIRCLE

            YOUWILLNEVERBEAFULLCIRCLE {
                orientation 0  (0.5 - (STOP1RADIALLENGTH / QUADRANTCIRCUMFERENCE))
            }
            YOUWILLNEVERBEAFULLCIRCLE {
                orientation 0  -(0.5 - (STOP1RADIALLENGTH / QUADRANTCIRCUMFERENCE))
            }

            /// Cut off a little bit more for the curvature
            YOUWILLNEVERBEAFULLCIRCLE {
                orientation 0  -0.5
            }
        }

        CURVATURE {
            orientation 0  0.5
        }
    }
}

define STOP2 {
    difference {
        FULLCIRCLE {
        }

        YOUWILLNEVERBEAFULLCIRCLE {
            orientation 0  (0.5 - (STOP2RADIALLENGTH / QUADRANTCIRCUMFERENCE))
        }
        YOUWILLNEVERBEAFULLCIRCLE {
            orientation 0  -(0.5 - (STOP2RADIALLENGTH / QUADRANTCIRCUMFERENCE))
        }
    }
}

/// Stop 1
difference {
    STOP1 {
        // size 0
        color 0  1  0
        opacity 0.75
        orientation 0  ((STOPSRADIALDISTANCE + (MIDDLESTOPPERTHICKNESS / 2)) / QUADRANTCIRCUMFERENCE + (STOP1RADIALLENGTH / QUADRANTCIRCUMFERENCE))
    }

    /// Screwholes
    SCREWHOLE {
        position -145.75 0 -131.5
    }
    SCREWHOLE {
        position -124.5 0 -76
    }
}

/// Stop 2
difference {
    STOP2 {
        // size 0
        color 0  1  0
        opacity 0.75
        orientation 0  -((STOPSRADIALDISTANCE + (MIDDLESTOPPERTHICKNESS / 2)) / QUADRANTCIRCUMFERENCE + (STOP2RADIALLENGTH / QUADRANTCIRCUMFERENCE))
    }

    /// Screws
    SCREWHOLE {
        position 105.5  0  -95
    }
    SCREWHOLE {
        position 163.75  0  -133.25
    }

    /// Nuts
    NUTHOLE {
        position 127.5  0  -134.5
    }
}

/// Quadrant's scan
SCAN {
    opacity 0.5
    size 0
    orientation 0  0  -0.5
}
/// Reference for aligning the scan
cube {
    color 0  1  1
    opacity 0.5
    size 0 // 52  104
}

//////////////////////////////////////////////////////////////////////////////

print("Total quadrant circumference: " QUADRANTCIRCUMFERENCE " mm")
print("Rudder turn angle: " 360 * (STOPSRADIALDISTANCE / QUADRANTCIRCUMFERENCE) " deg")
print("Middle stopper thickness: " MIDDLESTOPPERTHICKNESS " mm")
print("Radial distance between two stops: " (STOPSRADIALDISTANCE + MIDDLESTOPPERTHICKNESS) " mm")
