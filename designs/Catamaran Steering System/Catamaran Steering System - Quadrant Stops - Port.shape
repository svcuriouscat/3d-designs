// ShapeScript document

/// Catamaran Steering System - Quadrant Stops - Port

detail 256
rotate 0  0  -0.5  /// CAD

define PI                           3.14159265359
define INCHINMM                    25.399999619
/// Rudder
define RUDDERTURNANGLE             75       /// degrees
define RUDDERPOSTSHAFTSIZE         (INCHINMM * 2)  /// mm
/// Quadrant
define QUADRANTDIAMETER           400       /// mm
define QUADRANTCIRCUMFERENCE       (QUADRANTDIAMETER * PI)
define QUADRANTCENTEROFFSET         5       /// mm (towards center-midship, to create differential between rudders)
/// Fasteners
define SCREWDIAMETER                9.525   /// mm (3/8")
/// Stops
define STOPHEIGHT                  30       /// mm
define STOPWIDTH                   80       /// mm
define STOPRADIALLENGTH            (QUADRANTCIRCUMFERENCE / 2 - (QUADRANTCIRCUMFERENCE * (75 / 360)))  /// mm
/// Cable guard
define CABLEGUARDHEIGHTTHICKNESS    8
define CABLEGUARDWIDTH             20
define CABLEGUARDLENGTH            40

define SCAN {
    import "scans/quadrant-port.stl"
}

define FULLCIRCLE {
    difference {
        cylinder {
            size QUADRANTDIAMETER  STOPHEIGHT
        }

        cylinder {
            size (QUADRANTDIAMETER - STOPWIDTH * 2)  STOPHEIGHT
        }
    }
}

define YOUWILLNEVERBEAFULLCIRCLE {
    cube {
        size (QUADRANTDIAMETER)  (STOPHEIGHT)  (QUADRANTDIAMETER / 2)
        position 0  0  -(QUADRANTDIAMETER / 4)
    }
}

define SCREWHOLE {
    cylinder {
        color 100  0  0
        size (SCREWDIAMETER + 0.4)  (STOPHEIGHT)
        position 0  0  (QUADRANTDIAMETER / 2)
    }
}

define CABLEGUARDCUTOUT {
    union {
        position 0  (-STOPHEIGHT / 2 + CABLEGUARDHEIGHTTHICKNESS / 2)  (400 / 2 - CABLEGUARDLENGTH / 2)

        cube {
            size CABLEGUARDWIDTH  CABLEGUARDHEIGHTTHICKNESS  CABLEGUARDLENGTH
        }

        cylinder {
            size CABLEGUARDWIDTH  (STOPHEIGHT * 2)
            position 0  0  -(CABLEGUARDLENGTH / 2 - CABLEGUARDWIDTH / 2 - 4)
        }
    }
}

define WEIGHTCUTOUT {
    cylinder {
        size (STOPWIDTH / 2)  STOPHEIGHT
        position 0  0  (QUADRANTDIAMETER / 2 - STOPWIDTH / 2)
    }
}

define STOP {
    difference {
        FULLCIRCLE

        YOUWILLNEVERBEAFULLCIRCLE {
            orientation 0  (0.5 - (STOPRADIALLENGTH / QUADRANTCIRCUMFERENCE))
        }
        YOUWILLNEVERBEAFULLCIRCLE {
            orientation 0  -(0.5 - (STOPRADIALLENGTH / QUADRANTCIRCUMFERENCE))
        }

        /// Reduce part's weight
        //WEIGHTCUTOUT {
        //    orientation 0  0.1
        //}
        //WEIGHTCUTOUT {
        //}
        //WEIGHTCUTOUT {
        //    orientation 0  -0.1
        //}
    }
}

/// Stop
difference {
    STOP {
        color 1  0  0
        opacity 0.75
    }

    /// Screws
    SCREWHOLE {
        position -91.5  0  -64
    }
    SCREWHOLE {
        position 89  0  -63
    }

    CABLEGUARDCUTOUT {
        orientation 0  (0.5 - (STOPRADIALLENGTH / QUADRANTCIRCUMFERENCE))-0.0475
    }
}

/// Quadrant's scan
SCAN {
    opacity 0.5
    orientation 0  0  -0.5
    position -(QUADRANTCENTEROFFSET / 2)  -10  -(QUADRANTCENTEROFFSET / 2)
    //size 0
}
/// Reference for aligning the scan
cube {
    color 0  1  1
    opacity 0.5
    size RUDDERPOSTSHAFTSIZE  (RUDDERPOSTSHAFTSIZE * 2)
    position -(QUADRANTCENTEROFFSET / 2)  0  -(QUADRANTCENTEROFFSET / 2)
}

//////////////////////////////////////////////////////////////////////////////

print("Total quadrant circumference: " QUADRANTCIRCUMFERENCE " mm")
print("Rudder turn angle: " 360 * (((QUADRANTCIRCUMFERENCE / 2) - STOPRADIALLENGTH) / QUADRANTCIRCUMFERENCE) " deg")
print("Radial length of the stop: " (STOPRADIALLENGTH) " mm")
