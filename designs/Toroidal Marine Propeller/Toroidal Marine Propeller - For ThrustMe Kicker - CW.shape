// ShapeScript document

/// Toroidal Marine Propeller — For ThrustMe Kicker — Clockwise

//////////////////////////////////////////////////////////////////////////////

detail 64 * 2
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

/// Motor
define MOTORSPEED                 1900      /// RPM (what speed the prop's targeted at, https://www.thrustme.no/eu/thruster-more-eu claims 200–4200 RPM)
define MOTORSPINDIRECTION            1      /// direction, (1 for CW, -1 for CCW)
/// Propeller
define PROPDIAMETERWITHBLADES      102      /// mm
/// Propeller base
define PROPBASELENGTH              43.8     /// mm
define PROPBASEDIAMETER            46       /// mm
define PROPBASEWALLTHICKNESS        1.5     /// mm
define PROPBASETOPTHICKNESS         2.5     /// mm
define PROPBASETOPEDGERADIUS        1       /// mm
define PROPBASEHOLEDIAMETER         7.2     /// mm
/// Propeller blades
define PROPBLADECOUNT               3       /// count (total amount of toroidal blades)
define PROPBLADETHICKNESS           3
define PROPBLADEWIDTH              25
define PROPBLADESOFFSET             5
define PROPBLADESHEIGHT            27       /// mm (how much prop lenpth the blades take up)
/// Auto-calculated
define PROPBLADEPITCH              25       /// degrees (inital [base] angle of the blade) TODO: likely needs to use MOTORSPEED to self-define
define PROPBLADELENGTH             ((PROPDIAMETERWITHBLADES - PROPBASEDIAMETER) / 2)  /// mm

//////////////////////////////////////////////////////////////////////////////

define PROPBASE {
    difference {
        cylinder {
            size PROPBASEDIAMETER  PROPBASELENGTH
        }

        /// Main cutout
        cylinder {
            size PROPBASEDIAMETER - PROPBASEWALLTHICKNESS * 2  PROPBASELENGTH - PROPBASETOPTHICKNESS
            position 0 -(PROPBASETOPTHICKNESS / 2)
        }

        /// Middle hole
        cylinder {
            size PROPBASEHOLEDIAMETER  PROPBASETOPTHICKNESS
            position 0  (PROPBASELENGTH / 2 - PROPBASETOPTHICKNESS / 2)
        }

        /// Smooth edge
        difference {
            difference {
                cylinder {
                    size PROPBASEDIAMETER  PROPBASETOPEDGERADIUS
                }

                cylinder {
                    size PROPBASEDIAMETER - PROPBASETOPEDGERADIUS * 2  PROPBASETOPEDGERADIUS
                }

                position 0  PROPBASETOPEDGERADIUS / 2
            }

            extrude {
                circle {
                    size (PROPBASETOPEDGERADIUS * 2)
                }
                along circle {
                    orientation 0  0  0.5
                    size PROPBASEDIAMETER - PROPBASETOPEDGERADIUS * 2
                }
            }

            position 0  (PROPBASELENGTH / 2 - PROPBASETOPEDGERADIUS)
        }
    }
}

define PROPBLADESHAPE {
    circle {
        size (PROPBLADETHICKNESS)  (PROPBLADEWIDTH)  (0)
    }
}

define PROPBLADE {
    difference {
        cylinder {
            size (PROPBLADELENGTH * 1.9)  PROPBLADEWIDTH  PROPBLADELENGTH
        }

        cylinder {
            size ((PROPBLADELENGTH - PROPBLADETHICKNESS * 2) * 1.9)  (PROPBLADEWIDTH)  (PROPBLADELENGTH - PROPBLADETHICKNESS * 2)
        }

        position (PROPBASEDIAMETER / 2 + PROPBLADELENGTH / 2 - PROPBLADETHICKNESS)  0
        orientation 0  0  0.2

        size 0
    }

    difference {
        extrude {
            PROPBLADESHAPE {
            }
            along circle {
                // orientation 0  0  0.25
                size (PROPDIAMETERWITHBLADES - PROPBLADETHICKNESS)  (PROPBLADESHEIGHT - PROPBLADETHICKNESS)
            }

            // orientation 0  0  (PROPBLADEPITCH / 180)
        }

        cube {
            size (PROPDIAMETERWITHBLADES / 2)  (PROPDIAMETERWITHBLADES)  (PROPDIAMETERWITHBLADES)
            position (PROPDIAMETERWITHBLADES / 3)
        }

        orientation 0  0  (-(PROPBLADEPITCH / 180) * MOTORSPINDIRECTION)
    }
}

define PROP {
    PROPBASE

    difference {
        union {
            for i in 1 to PROPBLADECOUNT {
                PROPBLADE {
                    orientation 0  (2 / PROPBLADECOUNT * i)
                }
            }
        }

        cylinder {
            size (PROPBASEDIAMETER - PROPBASEWALLTHICKNESS * 2)  (PROPBASELENGTH - PROPBASETOPTHICKNESS)
            position 0  -(PROPBASETOPTHICKNESS / 2)
        }

        position 0  PROPBLADESOFFSET
    }
}

//////////////////////////////////////////////////////////////////////////////

PROP {
    color 50  50  50
}

cylinder {
    color 1  0  0
    opacity 0.25
    size PROPDIAMETERWITHBLADES  PROPBASELENGTH
    size 0
}

//////////////////////////////////////////////////////////////////////////////

print "[MOTOR] speed: " MOTORSPEED " RPM"
print "[PROP] pitch: " PROPBLADEPITCH " degrees"
