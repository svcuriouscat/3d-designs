// ShapeScript document

/// Orbital Sander Pad Clip â€” 150mm (6")

define BASEDIAMETER           150     /// mm (125 for 5" discs, 150 for 6" discs)
define BASEHEIGHT               3     /// mm
define BASEHOLEDIAMETER       BASEDIAMETER / 4.5  /// mm
define DISCHOLEDIAMETER         8     /// mm
define DISCOUTERHOLEDIAMETER   10     /// mm (N/A for 5" discs,  10 for 6" discs)
define DISCHOLEDISTANCE        57     /// mm (55 for 5" discs,   57 for 6" discs)
define DISCOUTERHOLEDISTANCE  110     /// mm (N/A for 5" discs, 110 for 6" discs)
define RODLENGTH               22     /// mm
define RODDIAMETER            DISCHOLEDIAMETER - 0.4  /// mm
define OUTERRODDIAMETER       DISCOUTERHOLEDIAMETER - 0.4  /// mm

detail 80
rotate 0 0 -0.5  /// CAD mode

define HOLE {
    difference {
        cylinder {
            size (BASEHOLEDIAMETER + BASEHEIGHT)  BASEHEIGHT
        }
        extrude {
            circle {
                size BASEHEIGHT
            }
            along circle {
                orientation 0  0  0.5
                size (BASEHOLEDIAMETER + BASEHEIGHT)
            }
        }
    }
}

define ROD {
    difference {
        cylinder {
            size RODDIAMETER  RODLENGTH
        }
        difference {
            cylinder {
                size RODDIAMETER  (RODDIAMETER / 2)
                position 0  (RODLENGTH / 2 - RODDIAMETER / 4)
            }
            sphere {
                size RODDIAMETER
                position 0  (RODLENGTH / 2 - RODDIAMETER / 2)
            }
        }
    }
}

define OUTERROD {
    difference {
        cylinder {
            size OUTERRODDIAMETER  RODLENGTH
        }
        difference {
            cylinder {
                size OUTERRODDIAMETER  (OUTERRODDIAMETER / 2)
                position 0  (RODLENGTH / 2 - OUTERRODDIAMETER / 4)
            }
            sphere {
                size OUTERRODDIAMETER
                position 0  (RODLENGTH / 2 - OUTERRODDIAMETER / 2)
            }
        }
    }
}

define PADCLIP {

    difference {
        /// Base with rods
        union {
            /// Base
            difference {
                union {
                    cylinder {
                        size (BASEDIAMETER - BASEHEIGHT)  BASEHEIGHT
                    }
                    /// Round outer edge
                    extrude {
                        circle {
                            size BASEHEIGHT
                        }
                        along circle {
                            orientation 0  0  0.5
                            size (BASEDIAMETER - BASEHEIGHT)
                        }
                    }
                }

                /// Middle hole
                HOLE

                /// Cut away from the outer edge
                union {
                    rotate 0  0.125

                    for i in 1 to 8 {
                        HOLE {
                            position (BASEDIAMETER / 2)
                        }

                        rotate 0  ((0.25 * i))
                    }
                }
            }

            /// Rods
            union {
                /// Inner rods
                for i in 1 to 8 {
                    ROD {
                        position (DISCHOLEDISTANCE / 2 + DISCHOLEDIAMETER / 2)  (BASEHEIGHT / 2 + RODLENGTH / 2)
                    }

                    rotate 0  (0.25 * i)
                }

                /// Outer rods
                for i in 1 to 8 {
                    OUTERROD {
                        position (DISCOUTERHOLEDISTANCE / 2 + DISCOUTERHOLEDIAMETER / 2)  (BASEHEIGHT / 2 + RODLENGTH / 2)
                    }

                    rotate 0  (0.25 * i)
                }
            }
        }
    }
    
}

PADCLIP
