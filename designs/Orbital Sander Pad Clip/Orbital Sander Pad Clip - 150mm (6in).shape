// ShapeScript document

/// Orbital Sander Pad Clip â€” 150mm (6")

//////////////////////////////////////////////////////////////////////////////

detail 90
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

define GAP                          0.2   /// mm

/// Base
define BASE_DIAMETER              150     /// mm (125 for 5" discs, 150 for 6" discs)
define BASE_HEIGHT                  3     /// mm
define BASE_HOLE_DIAMETER          (BASE_DIAMETER / 4.5)  /// mm

/// Disc
define DISC_HOLE_DIAMETER           8     /// mm
define DISC_HOLE_DISTANCE          57     /// mm (55 for 5" discs,   57 for 6" discs)
define DISC_OUTER_HOLES_DIAMETER   10     /// mm (N/A for 5" discs,  10 for 6" discs)
define DISC_OUTER_HOLES_DISTANCE  110     /// mm (N/A for 5" discs, 110 for 6" discs)

/// Rods
define ROD_LENGTH                  22     /// mm
define ROD_DIAMETER               (DISC_HOLE_DIAMETER - GAP * 2)  /// mm

/// Outer rods
define OUTER_ROD_DIAMETER         (DISC_OUTER_HOLES_DIAMETER - GAP * 2)  /// mm

//////////////////////////////////////////////////////////////////////////////

define HOLE {
    difference {
        cylinder {
            size (BASE_HOLE_DIAMETER + BASE_HEIGHT)  BASE_HEIGHT
        }
        extrude {
            circle {
                size BASE_HEIGHT
            }
            along circle {
                orientation 0  0  0.5
                size (BASE_HOLE_DIAMETER + BASE_HEIGHT)
            }
        }
    }
}

define ROD {
    difference {
        cylinder {
            size ROD_DIAMETER  ROD_LENGTH
        }
        difference {
            cylinder {
                size ROD_DIAMETER  (ROD_DIAMETER / 2)
                position 0  (ROD_LENGTH / 2 - ROD_DIAMETER / 4)
            }
            sphere {
                size ROD_DIAMETER
                position 0  (ROD_LENGTH / 2 - ROD_DIAMETER / 2)
            }
        }
    }
}

define OUTERROD {
    difference {
        cylinder {
            size OUTER_ROD_DIAMETER  ROD_LENGTH
        }
        difference {
            cylinder {
                size OUTER_ROD_DIAMETER  (OUTER_ROD_DIAMETER / 2)
                position 0  (ROD_LENGTH / 2 - OUTER_ROD_DIAMETER / 4)
            }
            sphere {
                size OUTER_ROD_DIAMETER
                position 0  (ROD_LENGTH / 2 - OUTER_ROD_DIAMETER / 2)
            }
        }
    }
}

define PADCLIP {
    difference {
        /// Base with rods
        union {
            /// Base
            difference {
                union {
                    cylinder {
                        size (BASE_DIAMETER - BASE_HEIGHT)  BASE_HEIGHT
                    }
                    /// Round outer edge
                    extrude {
                        circle {
                            size BASE_HEIGHT
                        }
                        along circle {
                            orientation 0  0  0.5
                            size (BASE_DIAMETER - BASE_HEIGHT)
                        }
                    }
                }

                /// Middle hole
                HOLE

                /// Cut away from the outer edge
                union {
                    rotate 0  0.125

                    for i in 1 to 8 {
                        HOLE {
                            position (BASE_DIAMETER / 2)
                        }

                        rotate 0  ((0.25 * i))
                    }
                }
            }

            /// Rods
            union {
                /// Inner rods
                for i in 1 to 4 {
                    ROD {
                        position (DISC_HOLE_DISTANCE / 2 + DISC_HOLE_DIAMETER / 2)  (BASE_HEIGHT / 2 + ROD_LENGTH / 2)
                    }

                    rotate 0  (i / 2)
                }

                /// Outer rods
                for i in 1 to 4 {
                    OUTERROD {
                        position (DISC_OUTER_HOLES_DISTANCE / 2 + DISC_OUTER_HOLES_DIAMETER / 2)  (BASE_HEIGHT / 2 + ROD_LENGTH / 2)
                    }

                    rotate 0  (i / 2)
                }
            }
        }
    }
}

//////////////////////////////////////////////////////////////////////////////

PADCLIP {
}

//////////////////////////////////////////////////////////////////////////////

print "[MODEL] detail: " (detail)
