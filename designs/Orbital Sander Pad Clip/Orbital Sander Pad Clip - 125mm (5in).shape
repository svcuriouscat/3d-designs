// ShapeScript document

/// Orbital Sander Pad Clip â€” 125mm (5")

define BASEDIAMETER          125     /// mm (125 for 5" discs, 150 for 6" discs)
define BASEHEIGHT              3     /// mm
define BASEHOLEDIAMETER      BASEDIAMETER / 3.5  /// mm
define DISCHOLEDIAMETER       10     /// mm
define DISCHOLEDISTANCE       55     /// mm (55 for 5" discs, ?? for 6" discs)
define RODDIAMETER           DISCHOLEDIAMETER - 0.4  /// mm
define RODLENGTH              22     /// mm

detail 80
rotate 0 0 -0.5  /// CAD mode

define HOLE {
    difference {
        cylinder {
            size (BASEHOLEDIAMETER + BASEHEIGHT)  BASEHEIGHT
        }
        extrude {
            circle {
                size BASEHEIGHT
            }
            along circle {
                orientation 0  0  0.5
                size (BASEHOLEDIAMETER + BASEHEIGHT)
            }
        }
    }
}

define ROD {
    difference {
        cylinder {
            size RODDIAMETER  RODLENGTH
        }
        difference {
            cylinder {
                size RODDIAMETER  (RODDIAMETER / 2)
                position 0  (RODLENGTH / 2 - RODDIAMETER / 4)
            }
            sphere {
                size RODDIAMETER
                position 0  (RODLENGTH / 2 - RODDIAMETER / 2)
            }
        }
    }
}

define PADCLIP {

    difference {
        /// Base with rods
        union {
            /// Base
            difference {
                union {
                    cylinder {
                        size (BASEDIAMETER - BASEHEIGHT)  BASEHEIGHT
                    }
                    /// Round outer edge
                    extrude {
                        circle {
                            size BASEHEIGHT
                        }
                        along circle {
                            orientation 0  0  0.5
                            size (BASEDIAMETER - BASEHEIGHT)
                        }
                    }
                }

                /// Middle hole
                HOLE

                /// Cut away from the outer edge
                union {
                    rotate 0  0.125

                    for i in 1 to 8 {
                        HOLE {
                            position (BASEDIAMETER / 2)
                        }

                        rotate 0  (0.25 * i)
                    }
                }
            }

            /// Rods
            union {
                for i in 1 to 8 {
                    ROD {
                        position (DISCHOLEDISTANCE / 2 + DISCHOLEDIAMETER / 2)  (BASEHEIGHT / 2 + RODLENGTH / 2)
                    }

                    rotate 0  (0.25 * i)
                }
            }
        }
    }
    
}

PADCLIP
