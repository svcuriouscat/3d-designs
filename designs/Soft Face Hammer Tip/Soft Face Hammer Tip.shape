// ShapeScript document

/// Soft Face Hammer Tip

detail 1024
rotate 0  0  -0.5  /// CAD mode

/// Hammer tip
define HAMMERTIPDIAMETER    25.4   /// mm (1")
define HAMMERTIPLENGTH      19.05  /// mm (3/4")
/// Bolt (M8, 12mm long)
define BOLTSHAFTDIAMETER     8.2   /// mm
define BOLTSHAFTLENGTH      11.8   /// mm
define BOLTCAPLENGTH         5     /// mm
define BOLTCAPDIAMETER      14.2   /// mm
define BOLTEXPOSUREDLENGTH   6     /// mm (how much the bolt's shaft sticks out)

define BOLT {
    union {
        /// Thread
        cylinder {
            size BOLTSHAFTDIAMETER  BOLTSHAFTLENGTH
        }
        /// Cap
        cylinder {
            detail 6
            size BOLTCAPDIAMETER  BOLTCAPLENGTH
            position 0  (BOLTSHAFTLENGTH / 2 + BOLTCAPLENGTH / 2)
        }
    }
}

define HAMMERTIP {
    difference {
        cylinder {
            size HAMMERTIPDIAMETER  HAMMERTIPLENGTH
        }

        BOLT {
            position 0  -(HAMMERTIPLENGTH / 2 + BOLTSHAFTLENGTH / 2 - (BOLTSHAFTLENGTH - BOLTEXPOSUREDLENGTH))
        }
    }
}

HAMMERTIP {
    opacity 0.5
}

/// Elevator pad
cylinder {
    size BOLTSHAFTDIAMETER  0.2
    position 0  -(HAMMERTIPLENGTH / 2 + BOLTEXPOSUREDLENGTH + 0.2 / 2)
}
