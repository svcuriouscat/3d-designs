// ShapeScript document

detail 256

define BASETHICKNESS        2     // mm
define BASEHEIGHT          14     // mm
define BASEWIDTH           20     // mm
define CYLINDERLENGTH       4     // mm
define CYLINDERDIAMETER     9.2   // mm
define CYLINDERGROVESIZE    1.68  // mm
define CYLINDERGROVEDEPTH   0.96  // mm
define BUMPSDIAMETER        0.56  // mm

define BASE {
    union {
        cylinder {
            size BASEHEIGHT BASETHICKNESS 
            orientation 0.5 0 0.5
            position BASEWIDTH/4
        }
        cube {
            size BASEWIDTH/2 BASEHEIGHT BASETHICKNESS
        }
        cylinder {
            size BASEHEIGHT BASETHICKNESS 
            orientation 0.5 0 0.5
            position -BASEWIDTH/4
        }
    }
}

define CYLINDER {
    difference {
        cylinder {
            size CYLINDERDIAMETER CYLINDERLENGTH 
            orientation 0.5 0 0.5
        }
        difference {
            cylinder {
                size CYLINDERDIAMETER CYLINDERGROVESIZE 
                orientation 0.5 0 0.5
            }
            cylinder {
                size CYLINDERDIAMETER-CYLINDERGROVEDEPTH-CYLINDERGROVEDEPTH CYLINDERGROVESIZE 
                orientation 0.5 0 0.5
            }
        }
    }
}

define BUMP {
    detail 16
    sphere {
        size BUMPSDIAMETER
        position CYLINDERDIAMETER/2
    }
}

define MC4DUSTCAPFEMALE {
    union {
        BASE
        CYLINDER {
            position 0 0 CYLINDERLENGTH/2+BASETHICKNESS/2
        }
        for i in 1 to 4 {
            BUMP {
                position 0 0 CYLINDERLENGTH+BASETHICKNESS/2-(CYLINDERLENGTH-CYLINDERGROVESIZE)/2/2
                orientation 0.5*i+0.25
            }
        }
    }
}

MC4DUSTCAPFEMALE {
    opacity 0.6
}
