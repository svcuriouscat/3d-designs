// ShapeScript document

// MC4 Dust Cap - For Female

detail 256

define BASETHICKNESS        1.4   // mm
define BASEHEIGHT          14     // mm
define BASEWIDTH           20     // mm
define CYLINDERLENGTH       4     // mm
define CYLINDERDIAMETER     9.2   // mm
define CYLINDERGROVESIZE    2     // mm
define CYLINDERGROVEDEPTH   1     // mm

define BASE {
    union {
        cylinder {
            size BASEHEIGHT BASETHICKNESS 
            orientation 0.5 0 0.5
            position BASEWIDTH/4
        }
        cube {
            size BASEWIDTH/2 BASEHEIGHT BASETHICKNESS
        }
        cylinder {
            size BASEHEIGHT BASETHICKNESS 
            orientation 0.5 0 0.5
            position -BASEWIDTH/4
        }
    }
}

define CYLINDER {
    difference {
        cylinder {
            size CYLINDERDIAMETER CYLINDERLENGTH 
            orientation 0.5 0 0.5
        }
        difference {
            cylinder {
                size CYLINDERDIAMETER CYLINDERGROVESIZE 
                orientation 0.5 0 0.5
            }
            cylinder {
                size CYLINDERDIAMETER-CYLINDERGROVEDEPTH-CYLINDERGROVEDEPTH CYLINDERGROVESIZE 
                orientation 0.5 0 0.5
            }
        }
    }
}

define MC4DUSTCAPFEMALE {
    union {
        BASE

        CYLINDER {
            position 0 0 CYLINDERLENGTH/2+BASETHICKNESS/2
        }
    }
}

MC4DUSTCAPFEMALE {
    opacity 0.6
}
