// ShapeScript document

/// MC4 Protective Cover - For Female

detail 256
rotate 0  0  -0.5  /// CAD mode

/// Base
define BASETHICKNESS        1.4   /// mm
define BASEHEIGHT          13     /// mm
define BASEWIDTH           19     /// mm
/// Cylinder
define CYLINDERLENGTH       4     /// mm
define CYLINDERDIAMETER     9.2   /// mm
define CYLINDERGROVESIZE    1.5   /// mm
define CYLINDERGROVEDEPTH   1     /// mm
/// Locks
define LOCKSLENGTH         11     /// mm
define LOCKSOUTERDISTANCE  15.9   /// mm

define BASE {
    intersection {
        cylinder {
            size BASEWIDTH  1
        }
        cube {
            size (BASEWIDTH)  1  (BASEHEIGHT)
        }
    }
}

define CYLINDER {
    difference {
        cylinder {
            size CYLINDERDIAMETER  CYLINDERLENGTH 
        }
        difference {
            cylinder {
                size CYLINDERDIAMETER  CYLINDERGROVESIZE 
            }
            cylinder {
                size (CYLINDERDIAMETER - CYLINDERGROVEDEPTH - CYLINDERGROVEDEPTH)  CYLINDERGROVESIZE 
            }
        }
    }
}

define LOCKS {
    difference {
        group {
            cube {
                size 1.3  LOCKSLENGTH  6.56
                position ((LOCKSOUTERDISTANCE - 1.3) / 2)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2)
            }
            cube {
                size 2.5  4  6.56
                position ((LOCKSOUTERDISTANCE - 1.3) / 2 + 0.6)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2 + LOCKSLENGTH / 2 - 4 / 2)
            }
        }

        cube {
            size 2.5  8  6.56
            orientation -0.175
            position (((LOCKSOUTERDISTANCE - 1.3) / 2 + 0.0) + 1.9)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2 + LOCKSLENGTH / 2 - 4 / 2)
        }
    }

    difference {
        group {
            cube {
                size 1.3  LOCKSLENGTH  6.56
                position -((LOCKSOUTERDISTANCE - 1.3) / 2)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2)
            }
            cube {
                size 2.5  4  6.56
                position -((LOCKSOUTERDISTANCE - 1.3) / 2 + 0.6)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2 + LOCKSLENGTH / 2 - 4 / 2)
            }
        }

        cube {
            size 2.5  8  6.56
            orientation 0.175
            position -(((LOCKSOUTERDISTANCE - 1.3) / 2 + 0.0) + 1.9)  (LOCKSLENGTH / 2 + BASETHICKNESS / 2 + LOCKSLENGTH / 2 - 4 / 2)
        }
    }
}

define MC4PROTECTIVECAPFEMALE {
    union {
        BASE {
            size 1  BASETHICKNESS
        }

        CYLINDER {
            position 0 (CYLINDERLENGTH / 2 + BASETHICKNESS / 2)
        }

        LOCKS
    }
}

MC4PROTECTIVECAPFEMALE {
}
