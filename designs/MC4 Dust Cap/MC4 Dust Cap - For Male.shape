// ShapeScript document

/// MC4 Protective Cover - For Male

detail 256
rotate 0  0  -0.5  /// CAD mode

/// Base
define BASETHICKNESS        1.4    /// mm
define BASEHEIGHT          13      /// mm
define BASEWIDTH           19      /// mm
/// Chamber
define HOLEDIAMETER        10      /// mm
define CHAMBERLENGTH       24      /// mm
/// Slots for locking tabs
define SLOTLENGTH           7      /// mm
define SLOTTHICKNESS        2.2    /// mm
define SLOTWIDTH            7      /// mm
define SLOTSINNERDISTANCE  11.5    /// mm
define SLOTSCUTOUTSLENGTH   4      /// mm

define BASE {
    intersection {
        cylinder {
            size BASEWIDTH  1
        }
        cube {
            size (BASEWIDTH)  1  (BASEHEIGHT)
        }
    }
}

define MC4PROTECTIONFORMALE {
    difference {
        BASE {
            size 1  (CHAMBERLENGTH + BASETHICKNESS)
        }
        cylinder {
            size HOLEDIAMETER  CHAMBERLENGTH
            position 0  (BASETHICKNESS / 2)
        }

        /// Slots
        cube {
            size SLOTTHICKNESS  SLOTLENGTH  SLOTWIDTH
            position (SLOTSINNERDISTANCE / 2 + SLOTTHICKNESS / 2)  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2)
        }
        cube {
            size SLOTTHICKNESS  SLOTLENGTH  SLOTWIDTH
            position -(SLOTSINNERDISTANCE / 2 + SLOTTHICKNESS / 2)  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2)
        }

        /// Side cutouts
        cube {
            size ((BASEWIDTH - SLOTSINNERDISTANCE) / 2)  SLOTSCUTOUTSLENGTH  BASEHEIGHT
            position (SLOTSINNERDISTANCE / 2 + ((BASEWIDTH - SLOTSINNERDISTANCE) / 4))  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2 - SLOTLENGTH / 2 - SLOTSCUTOUTSLENGTH / 2)
        }
        cube {
            size ((BASEWIDTH - SLOTSINNERDISTANCE) / 2)  SLOTSCUTOUTSLENGTH  BASEHEIGHT
            position -(SLOTSINNERDISTANCE / 2 + ((BASEWIDTH - SLOTSINNERDISTANCE) / 4))  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2 - SLOTLENGTH / 2 - SLOTSCUTOUTSLENGTH / 2)
        }

        /// Side cutouts
        cube {
            size ((BASEWIDTH - SLOTSINNERDISTANCE))  SLOTSCUTOUTSLENGTH  BASEHEIGHT
            orientation 0.25
            position (SLOTSINNERDISTANCE / 2 + ((BASEWIDTH - SLOTSINNERDISTANCE) / 1.8446))  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2 - SLOTLENGTH / 2 - SLOTSCUTOUTSLENGTH / 2 - SLOTSCUTOUTSLENGTH) + 0.765
        }
        cube {
            size ((BASEWIDTH - SLOTSINNERDISTANCE))  SLOTSCUTOUTSLENGTH  BASEHEIGHT
            orientation -0.25
            position -(SLOTSINNERDISTANCE / 2 + ((BASEWIDTH - SLOTSINNERDISTANCE) / 1.8446))  (CHAMBERLENGTH / 2 - SLOTLENGTH / 2 + BASETHICKNESS / 2 - SLOTLENGTH / 2 - SLOTSCUTOUTSLENGTH / 2 - SLOTSCUTOUTSLENGTH) + 0.765
        }
    }
}

MC4PROTECTIONFORMALE {
}
