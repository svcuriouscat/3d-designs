// ShapeScript document

/// Rope End Cap / Aglet

detail 90
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

define INCH_IN_MM             25.399999619
/// Rope
define ROPE_THICKNESS          {  /// mm
     14  /// mm (14mm rope)
    // (1/2 * INCH_IN_MM)  /// mm (1/2" rope)
    // 12  /// mm (12mm rope)
    // 10  /// mm (10mm rope)
    // (3/8 * INCH_IN_MM)  /// mm (3/8" rope)
}
/// Cap
define CAP_WALL_THICKNESS      0.5   /// mm

//////////////////////////////////////////////////////////////////////////////

define ROPE_CAP {
    difference {
        union {
            cylinder {
                size (ROPE_THICKNESS + CAP_WALL_THICKNESS * 2)  (ROPE_THICKNESS * 1.5)
            }
            sphere {
                size (ROPE_THICKNESS + CAP_WALL_THICKNESS * 2)
                position 0  ((ROPE_THICKNESS * 1.5) / 2)
            }
        }

        union {
            cylinder {
                size (ROPE_THICKNESS)  (ROPE_THICKNESS * 1.5)
            }
            sphere {
                size (ROPE_THICKNESS)
                position 0  ((ROPE_THICKNESS * 1.5) / 2)
            }
        }
    }
}

//////////////////////////////////////////////////////////////////////////////

ROPE_CAP {
    opacity 0.5
}

//////////////////////////////////////////////////////////////////////////////

print "[ROPE] thickness: " ROPE_THICKNESS "mm"
print "[CAP] wall thickness: " CAP_WALL_THICKNESS "mm"
print "[CAP] inner diameter: " (ROPE_THICKNESS) "mm"
print "[CAP] outer diameter: " (ROPE_THICKNESS + CAP_WALL_THICKNESS * 2) "mm"
print "[CAP] total length: " ((ROPE_THICKNESS * 1.5) + (ROPE_THICKNESS - CAP_WALL_THICKNESS * 2) / 2) "mm"
