// ShapeScript document

/// Metal Container Protection - 1 Gal. Can Top Cover

detail 256
rotate 0 0 -0.5  /// CAD mode

define CANRADIUSWITHFLARE          168.2   /// mm
define CANFLAREHEIGHT                6     /// mm
define CANFLARETHICKNESS             1.27
define CANFLAREHEIGHTWITHOVERHANG    (CANFLAREHEIGHT + CANFLARETHICKNESS)     /// mm
define CANRADIUS                     (CANRADIUSWITHFLARE - CANFLARETHICKNESS * 2)  /// mm
define GASKETTHICKNESS               2     /// mm
define GASKETHEIGHT                  (CANFLAREHEIGHTWITHOVERHANG + GASKETTHICKNESS * 2)  /// mm

define GASKET {
    difference {
        cylinder {
            size (CANRADIUS + GASKETTHICKNESS * 2)  GASKETHEIGHT
        }

        /// Hole
        group {
            /// Can
            cylinder {
                size CANRADIUS  (GASKETTHICKNESS + CANFLAREHEIGHTWITHOVERHANG)
                position 0  (GASKETHEIGHT / 2 - (GASKETTHICKNESS + CANFLAREHEIGHTWITHOVERHANG) / 2)
            }

            // Middle (flare)
            difference {
                cylinder {
                    size CANRADIUSWITHFLARE  (CANFLAREHEIGHTWITHOVERHANG / 2)
                    position 0  (CANFLAREHEIGHTWITHOVERHANG / 2 / 2)
                }
                difference {
                    cylinder {
                        size CANRADIUSWITHFLARE  (CANFLAREHEIGHTWITHOVERHANG / 4)
                        position 0  (CANFLAREHEIGHTWITHOVERHANG / 2 - (CANFLAREHEIGHTWITHOVERHANG / 4 / 2))
                    }
                    cylinder {
                        size CANRADIUS  (CANFLAREHEIGHTWITHOVERHANG / 4)
                        position 0  (CANFLAREHEIGHTWITHOVERHANG / 2 - (CANFLAREHEIGHTWITHOVERHANG / 4 / 2))
                    }
                    extrude {
                        circle {
                            size (CANFLARETHICKNESS * 2)
                        }
                        along circle {
                            orientation 0  0  0.5
                            position 0  (CANFLAREHEIGHTWITHOVERHANG / 2) - (CANFLARETHICKNESS)
                            size CANRADIUS
                        }
                    }
                }
            }
            cylinder {
                size CANRADIUSWITHFLARE  (CANFLAREHEIGHTWITHOVERHANG / 2)
                position 0  -(CANFLAREHEIGHTWITHOVERHANG / 2 / 2)
            }
        }
    }
}

GASKET {
}
