// ShapeScript document

/// MC4 Protective Cover - For Female

detail 320
rotate 0  0  -0.5  /// CAD mode

/// Base
define BASELENGTH           5     /// mm
define BASEHEIGHT          11     /// mm
define BASEDIAMETER        19     /// mm
define BASEHOLEDIAMETER     3     /// mm
/// Cylinder
define CYLINDERLENGTH       4     /// mm
define CYLINDERDIAMETER     9.2   /// mm
define CYLINDERGROVESIZE    1.5   /// mm
define CYLINDERGROVEDEPTH   1     /// mm
/// Locks
define LOCKSTHICKNESS       1.5   /// mm
define LOCKSWIDTH           6.65  /// mm
define LOCKSLENGTH         11     /// mm
define LOCKSOUTERDISTANCE  15.9   /// mm
define LOCKSTABLENGTH       3.84  /// mm
define LOCKTABTHICKNESS     2     /// mm
/// Shaft
define SHAFTLENGTH         (LOCKSLENGTH - CYLINDERLENGTH)     /// mm
define SHAFTDIAMETER        8     /// mm
define SHAFTWIDTH           7     /// mm
define SHAFTHOLEDIAMETER    5     /// mm

define BASE {
    difference {
        intersection {
            cylinder {
                size BASEDIAMETER  BASELENGTH
            }
            cube {
                size BASEDIAMETER  BASELENGTH  BASEHEIGHT
            }
        }

        cylinder {
            size BASEHOLEDIAMETER  BASEHEIGHT
            orientation 0  0  0.5
        }
    }
}

define CYLINDER {
    difference {
        cylinder {
            size CYLINDERDIAMETER  CYLINDERLENGTH 
        }
        difference {
            cylinder {
                size CYLINDERDIAMETER  CYLINDERGROVESIZE 
            }
            cylinder {
                size (CYLINDERDIAMETER - CYLINDERGROVEDEPTH - CYLINDERGROVEDEPTH)  CYLINDERGROVESIZE 
            }
        }
    }
}

define LOCK {
    difference {
        group {
            cube {
                size LOCKSTHICKNESS  LOCKSLENGTH  LOCKSWIDTH
            }
            cube {
                size LOCKTABTHICKNESS  LOCKSTABLENGTH  LOCKSWIDTH
                position ((LOCKTABTHICKNESS - LOCKSTHICKNESS) / 2)  (LOCKSLENGTH / 2 - LOCKSTABLENGTH / 2)
            }
        }

        cube {
            size 2.5  8  LOCKSWIDTH
            orientation -0.175
            position (1.9)  (LOCKSLENGTH / 2 - 4 / 2)
        }
    }
}

define LOCKS {
    LOCK {
        position ((LOCKSOUTERDISTANCE - 1.6) / 2)  (LOCKSLENGTH / 2 + BASELENGTH / 2) - 0.025
        orientation 0.01
    }

    LOCK {
        position -((LOCKSOUTERDISTANCE - 1.6) / 2)  (LOCKSLENGTH / 2 + BASELENGTH / 2) - 0.025
        orientation -0.01  1
    }

    /// Visual aid
    cube {
        opacity 0.3
        color 1  0  0
        size 15.5  LOCKSTHICKNESS  LOCKSWIDTH
        size 0
        position 0  (BASELENGTH / 2 + LOCKSTHICKNESS / 2)
    }
    cube {
        opacity 0.3
        color 1  0  0
        size 17  LOCKSTHICKNESS  LOCKSWIDTH
        size 0
        position 0  (BASELENGTH / 2 + LOCKSTHICKNESS / 2 + LOCKSLENGTH / 1.5)
    }
}

define SHAFT {
    //difference {
        intersection {
            cylinder {
                size SHAFTDIAMETER  SHAFTLENGTH
            }

            cube {
                size SHAFTWIDTH  SHAFTLENGTH  SHAFTDIAMETER
            }
        }

        //cylinder {
        //    size SHAFTHOLEDIAMETER  SHAFTLENGTH
        //}
    //}
}

define MC4PROTECTIVECAPFEMALE {
    union {
        BASE {
        }

        CYLINDER {
            position 0  (BASELENGTH / 2 + CYLINDERLENGTH / 2)
        }

        SHAFT {
            position 0  (BASELENGTH / 2 + CYLINDERLENGTH + SHAFTLENGTH / 2)
        }

        LOCKS {
        }
    }
}

MC4PROTECTIVECAPFEMALE {
    opacity 0.5

    /// Lay on a side
    // orientation 0.5  0.5
}
