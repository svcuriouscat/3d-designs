// ShapeScript document

/// Catamaran Propulsion System — Starboard Motor
/// Motor: E-Tech 10WG

//////////////////////////////////////////////////////////////////////////////

detail 64
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

/// Motor
define MOTORBODYDIAMETER                     206.5     /// mm
define MOTORBODYLENGTH                       448       /// mm
define MOTORSHAFTLENGTH                       60       /// mm
define MOTORSHAFTDIAMETER                     35       /// mm
/// Gasket
define GASKETTHICKNESS                         0.4     /// mm (my own addition to protect the blue paint on the aluminum chassis of the motor)
/// Brackets
define BRACKETLEGSIZE                         40       /// mm
define BRACKETTHICKNESS                        4       /// mm
define BRACKETWIDTH                          340       /// mm (cut from E-Tech's 50cm)
define BRACKETLEGLENGTH                      271       /// mm (length of the legs, from horizontal rail all the way to the tips)
define BRACKETHOLESDISTANCE                  300       /// mm (varies slightly per bracket, 299–304)
define BRACKETHOLESOFFSET                     (15.05 - 13.05)  /// mm (offset of screw holes)
define BRACKETDISTANCEFROMSHAFT              165.5     /// mm
/// Anti-vibration mount
define ANTIVIBRATIONMOUNTHEIGHT               40       /// mm
define ANTIVIBRATIONMOUNTBASEPLATELENGTH     120.75    /// mm
define ANTIVIBRATIONMOUNTBASEPLATEWIDTH       60.5     /// mm
define ANTIVIBRATIONMOUNTBASEPLATETHICKNESS    3       /// mm
define ANTIVIBRATIONMOUNTSCREWHOLEWIDTH       13.85    /// mm
define ANTIVIBRATIONMOUNTSCREWHOLELENGTH      11       /// mm
define ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE   100       /// mm
/// Coupling
define COUPLINGDIAMETER                      101.6     /// mm
/// Floor hatch
define FLOORHATCHWIDTH                       350       /// mm
define FLOORHATCHLENGTH                      760       /// mm
/// Rails
define RAILLENGTH                            614       /// mm (24" + bonus extra length)
define RAILLEGSIZE                            63.5     /// mm (2-1/2", 2.5")
define RAILTHICKNESS                           6.35    /// mm (1/4", 0.25")
define RAILANTIVIBRATIONMOUNTSCREWHOLESIZE     9.525   /// mm (3/8")
define RAILVERTICALPOSITIONOFFSET             85       /// mm (distance between motor's horizontal brackets and rails)
/// Supports
define SUPPORTTHICKNESS                       50       /// mm
define SUPPORTWIDTH                          100       /// mm
define SUPPORTHEIGHT                         150       /// mm
define SUPPORTANGLERADIUS                     20       /// mm
define SUPPORTFIBERGLASSTHICKNESS              2       /// mm
define SUPPORTHOLEDIAMETER                    10       /// mm
/// 

//////////////////////////////////////////////////////////////////////////////

/// L-Shaped aluminum (6061) angle (2.5" x 2.5" x 24", 0.25" thick)
define RAIL {
    color 145  145  145

    difference {
        /// Shape
        cube {
            size RAILLEGSIZE  RAILLENGTH
        }
        cube {
            size (RAILLEGSIZE - RAILTHICKNESS)  RAILLENGTH
            position (RAILTHICKNESS / 2)  0  -(RAILTHICKNESS / 2)
        }

        /// Round top edge
        difference {
            cube {
                size RAILTHICKNESS  RAILLENGTH  RAILTHICKNESS / 2
                position 0  0  -(RAILTHICKNESS / 4)
            }
            cylinder {
                size RAILTHICKNESS  RAILLENGTH
            }

            position -(RAILLEGSIZE / 2 - RAILTHICKNESS / 2)  0  -(RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
        }

        /// Round bottom edge
        difference {
            cube {
                size (RAILTHICKNESS / 2)  RAILLENGTH  RAILTHICKNESS
                position (RAILTHICKNESS / 4)
            }
            cylinder {
                size RAILTHICKNESS  RAILLENGTH
            }

            position (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)  0  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
        }

        /// Screw holes
        group {
            cylinder {
                size RAILANTIVIBRATIONMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  (ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }
            cylinder {
                size RAILANTIVIBRATIONMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  -(ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }

            position 0  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        group {
            cylinder {
                size RAILANTIVIBRATIONMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  (ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }
            cylinder {
                size RAILANTIVIBRATIONMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  -(ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }

            position 0  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }

        /// Align top surface of the rail with the motor's center
        position 0  0  -(RAILLEGSIZE / 2)
    }
}

/// The electric motor
/// https://starboats.eu/electric-drives/e-tech-wg-inboard-engines/
define MOTOR {
    color 0  0  1

    union {
        /// Body
        cylinder {
            size MOTORBODYDIAMETER  MOTORBODYLENGTH
        }

        /// Shaft
        cylinder {
            color 100  100  100
            opacity 0.8

            size MOTORSHAFTDIAMETER  MOTORSHAFTLENGTH

            position 0  (MOTORBODYLENGTH / 2 + MOTORSHAFTLENGTH / 2)
        }

        /// Bump in the back
        cylinder {
            size 100  24
            position 0  -(MOTORBODYLENGTH / 2 + 24 / 2)
        }
    }
}

define BRACKETRAIL {
    color 100  100  100

    difference {
        /// Shape
        cube {
            size BRACKETLEGSIZE  1
        }
        cube {
            size (BRACKETLEGSIZE - BRACKETTHICKNESS)  1
            position (BRACKETTHICKNESS / 2)  0  -(BRACKETTHICKNESS / 2)
        }

        /// Round top edge
        difference {
            cube {
                size BRACKETTHICKNESS  1  BRACKETTHICKNESS / 2
                position 0  0  -(BRACKETTHICKNESS / 4)
            }
            cylinder {
                size BRACKETTHICKNESS
            }

            position -(BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)  0  -(BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)
        }

        /// Round bottom edge
        difference {
            cube {
                size (BRACKETTHICKNESS / 2)  1  BRACKETTHICKNESS
                position (BRACKETTHICKNESS / 4)
            }
            cylinder {
                size BRACKETTHICKNESS
            }

            position (BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)  0  (BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)
        }
    }
}

/// Stainless steel brackets bolted to both ends of the motor
define BRACKET {
    rotate 0.5

    union {
        /// Horizontal
        difference {
            BRACKETRAIL {
                size 1  BRACKETWIDTH
                orientation 1
            }

            /// Holes
            cylinder {
                orientation 0  0  0.5
                size 12  BRACKETTHICKNESS
                position -(BRACKETHOLESOFFSET)  (BRACKETHOLESDISTANCE / 2)  (BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)
            }
            cylinder {
                orientation 0  0  0.5
                size 12  BRACKETTHICKNESS
                position -(BRACKETHOLESOFFSET)  -(BRACKETHOLESDISTANCE / 2)  (BRACKETLEGSIZE / 2 - BRACKETTHICKNESS / 2)
            }
        }

        difference {
            union {
                /// Vertical (right)
                BRACKETRAIL {
                    size 1  BRACKETLEGLENGTH
                    orientation 0.5  0  0.5
                    position 0  (115 / 2 + BRACKETLEGSIZE / 2)  (BRACKETLEGLENGTH / 2 + BRACKETLEGSIZE / 2)
                }

                /// Vertical (left)
                BRACKETRAIL {
                    size 1  BRACKETLEGLENGTH
                    orientation 1  0  0.5
                    position 0  -(115 / 2 + BRACKETLEGSIZE / 2)  (BRACKETLEGLENGTH / 2 + BRACKETLEGSIZE / 2)
                }
            }

            /// Round cut-offs to fit into narrow bilge
            difference {
                cube {
                    size BRACKETLEGSIZE  MOTORBODYDIAMETER  MOTORBODYDIAMETER / 2
                    position 0  0  (BRACKETLEGLENGTH / 2 + BRACKETLEGSIZE / 2 + (BRACKETLEGLENGTH - MOTORBODYDIAMETER) / 2 + 14) + MOTORBODYDIAMETER / 4
                }
                cylinder {
                    size MOTORBODYDIAMETER  BRACKETLEGSIZE
                    orientation 0.5
                    position 0  0  (BRACKETLEGLENGTH / 2 + BRACKETLEGSIZE / 2 + (BRACKETLEGLENGTH - MOTORBODYDIAMETER) / 2 + 14)
                }
            }
        }

        /// Align bottom surface of the bracket with the motor's center
        position 0  0  -(BRACKETLEGSIZE / 2)
    }
}

define ANTIVIBRATIONMOUNT {
    color 200  200  200

    union {
        /// Core
        cube {
            size ANTIVIBRATIONMOUNTBASEPLATEWIDTH  80  ANTIVIBRATIONMOUNTHEIGHT
        }

        /// Base plate
        difference {
            cube {
                size ANTIVIBRATIONMOUNTBASEPLATEWIDTH  ANTIVIBRATIONMOUNTBASEPLATELENGTH  3
            }

            /// Screw holes
            cylinder {
                size ANTIVIBRATIONMOUNTSCREWHOLELENGTH  ANTIVIBRATIONMOUNTBASEPLATETHICKNESS
                orientation 0  0  0.5
                position 0  (ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)
            }
            cylinder {
                size ANTIVIBRATIONMOUNTSCREWHOLELENGTH  ANTIVIBRATIONMOUNTBASEPLATETHICKNESS
                orientation 0  0  0.5
                position 0  -(ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE / 2)
            }

            position 0  0  (ANTIVIBRATIONMOUNTHEIGHT / 2 - ANTIVIBRATIONMOUNTBASEPLATETHICKNESS / 2)
        }

        /// Central screw
        cylinder {
            opacity 0.8
            color 125  125  125
            orientation 0  0  0.5
            size 12  70
            position 0  0  -(ANTIVIBRATIONMOUNTHEIGHT / 2 + 70 / 2)
        }

        /// Nut
        cylinder {
            color 125  125  125
            size 31.35  10
            detail 6
            orientation 0  0  0.5
            position 0  0  -(ANTIVIBRATIONMOUNTHEIGHT / 2 + 10 / 2)
        }

        /// Align bottom surface of anti-vibration mounts with the motor's center
        position 0  0  -(ANTIVIBRATIONMOUNTHEIGHT / 2)
    }
}

define SUPPORT {
    color 0  100  0
    opacity 0.8

    union {
        cube {
            size (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT)
        }
        cube {
            size (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
        }
        cube {
            size (SUPPORTWIDTH)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
        }

        group {
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }

            position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
        }
        group {
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }

            position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
        }
        group {
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
        }
        group {
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
        }
        group {
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
        }

        /// Align top surface of supports with the motor's center
        position 0  0  (SUPPORTHEIGHT / 2)
    }
}

define MOTORWITHBRACKETS {
    MOTOR

    /// Brackets
    group {
        /// Front bracket
        BRACKET {
            position 0  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + GASKETTHICKNESS)
        }

        /// Back bracket
        BRACKET {
            orientation 1
            position 0  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + GASKETTHICKNESS)
        }

        /// Move up where it's supposed to be
        position 0  0  -(BRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2)
    }
}

//////////////////////////////////////////////////////////////////////////////

MOTORWITHBRACKETS {
}

/// Rails
group {
    RAIL {
        orientation 1  1
        position (BRACKETHOLESDISTANCE / 2)
    }
    RAIL {
        orientation 0  1
        position -(BRACKETHOLESDISTANCE / 2)
    }

    position 0  0  -(BRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET)
}

/// Anti-vibration mounts
group {
    ANTIVIBRATIONMOUNT {
        position 150  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    ANTIVIBRATIONMOUNT {
        position -150  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    ANTIVIBRATIONMOUNT {
        position 150  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    ANTIVIBRATIONMOUNT {
        position -150  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
    }

    position 0  0  -(BRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET)
}

/// Supports
difference {
    group {
        SUPPORT {
            position -(BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position (BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position -(BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position (BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + BRACKETLEGSIZE / 2 + BRACKETHOLESOFFSET + GASKETTHICKNESS)
        }

        position 0  0  -(BRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILTHICKNESS - RAILVERTICALPOSITIONOFFSET - SUPPORTFIBERGLASSTHICKNESS)
    }

    /// Holes for securing rails
    group {
        cylinder {
            size (SUPPORTHOLEDIAMETER)  (MOTORBODYLENGTH * 2)
            position (BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS - 20)
        }
        cylinder {
            size (SUPPORTHOLEDIAMETER)  (MOTORBODYLENGTH * 2)
            position -(BRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS - 20)
        }

        position 0  0  -(BRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILLEGSIZE / 2 - RAILVERTICALPOSITIONOFFSET - RAILTHICKNESS)
    }
}

/// Floor hatch
cube {
    opacity 0.25
    color 1  0  0
    size FLOORHATCHWIDTH  FLOORHATCHLENGTH  30
    position 0  0  -200
    size 0
}

//////////////////////////////////////////////////////////////////////////////

print "[RAIL] distance from end to nearest anti-vibration mount screw hole's center: " ((RAILLENGTH - MOTORBODYLENGTH - BRACKETLEGSIZE - ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE) / 2 - (BRACKETHOLESOFFSET + GASKETTHICKNESS)) "mm"
print "[RAIL] distance from end to farthest anti-vibration mount screw hole's center: " ((RAILLENGTH - MOTORBODYLENGTH - BRACKETLEGSIZE + ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE) / 2 - (BRACKETHOLESOFFSET + GASKETTHICKNESS)) "mm"
print "[RAIL] distance between innermost anti-vibration mount screw holes' centers: " (MOTORBODYLENGTH + BRACKETLEGSIZE - ANTIVIBRATIONMOUNTSCREWHOLEDISTANCE + ((BRACKETHOLESOFFSET + GASKETTHICKNESS) * 2)) "mm"
print "[RAIL] central anti-vibration mount screw holes' line: " (RAILLEGSIZE / 2) "mm"
print "[SUPPORTS] distance between supports: " (165 * 2 - SUPPORTTHICKNESS) "mm"
