// ShapeScript document

/// Catamaran Propulsion System — Starboard Motor
/// Motor: E-Tech 10WG

//////////////////////////////////////////////////////////////////////////////

detail 64
rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

/// Motor
define MOTORBODYDIAMETER                     206.5     /// mm
define MOTORBODYLENGTH                       448       /// mm
define MOTORSHAFTLENGTH                       60       /// mm
define MOTORSHAFTDIAMETER                     35       /// mm
/// Gasket
define GASKETTHICKNESS                         0.4     /// mm (my own addition to protect the blue paint on the aluminum chassis of the motor)
/// Motor brackets
define MOTORBRACKETLEGSIZE                    40       /// mm
define MOTORBRACKETTHICKNESS                   4       /// mm
define MOTORBRACKETWIDTH                     340       /// mm (cut from E-Tech's 50cm)
define MOTORBRACKETLEGLENGTH                 271       /// mm (length of the legs, from horizontal rail all the way to the tips)
define MOTORBRACKETHOLESDISTANCE             300       /// mm (varies slightly per bracket, 299–304)
define MOTORBRACKETHOLESOFFSET                (15.05 - 13.05)  /// mm (offset of screw holes)
define MOTORBRACKETDISTANCEFROMSHAFT         165.5     /// mm
/// Engine mount
define ENGINEMOUNT                            40       /// mm
define ENGINEMOUNTBASEPLATELENGTH            120.75    /// mm
define ENGINEMOUNTBASEPLATEWIDTH              60.5     /// mm
define ENGINEMOUNTBASEPLATETHICKNESS           3       /// mm
define ENGINEMOUNTSCREWHOLEWIDTH              13.85    /// mm
define ENGINEMOUNTSCREWHOLELENGTH             11       /// mm
define ENGINEMOUNTSCREWHOLEDISTANCE           89.5       /// mm
/// Coupling
define COUPLINGDIAMETER                      101.6     /// mm
/// Floor hatch
define FLOORHATCHWIDTH                       350       /// mm
define FLOORHATCHLENGTH                      760       /// mm
/// Rails
define RAILLENGTH                            610       /// mm (24")
define RAILLEGSIZE                            63.5     /// mm (2-1/2" AKA 2.5")
define RAILTHICKNESS                           6.35    /// mm (1/4" AKA 0.25")
define RAILMOUNTSCREWHOLESIZE                  9.525   /// mm (3/8")
define RAILVERTICALPOSITIONOFFSET             85       /// mm (distance between motor's horizontal brackets and rails)
/// Supports for rails
define SUPPORTTHICKNESS                       50       /// mm
define SUPPORTWIDTH                          100       /// mm
define SUPPORTHEIGHT                         150       /// mm
define SUPPORTANGLERADIUS                     20       /// mm
define SUPPORTFIBERGLASSTHICKNESS              2       /// mm
define SUPPORTHOLEDIAMETER                    10       /// mm
/// Holders
define HOLDERLONGTITUDALHOLEOFFSET            20       /// mm (distance from the center of the hole to the support)
define HOLDERTHICKNESS                         7       /// mm
define HOLDERLENGTH                           40       /// mm
define HOLDERWIDTH                            50       /// mm
define HOLDERHEIGHT                           55
/// Pre-calculated values
define RAILLOADMIDDLE                         ((RAILLEGSIZE - RAILTHICKNESS) / 2)  // (middle of the rail sans the leg)

//////////////////////////////////////////////////////////////////////////////

/// L-Shaped aluminum (6061) angle (2.5" x 2.5" x 24", 0.25" thick)
define RAIL {
    color 145  145  145

    difference {
        /// Shape
        cube {
            size RAILLEGSIZE  RAILLENGTH
        }
        cube {
            size (RAILLEGSIZE - RAILTHICKNESS)  RAILLENGTH
            position (RAILTHICKNESS / 2)  0  -(RAILTHICKNESS / 2)
        }

        /// Round top edge
        difference {
            cube {
                size RAILTHICKNESS  RAILLENGTH  RAILTHICKNESS / 2
                position 0  0  -(RAILTHICKNESS / 4)
            }
            cylinder {
                size RAILTHICKNESS  RAILLENGTH
            }

            position -(RAILLEGSIZE / 2 - RAILTHICKNESS / 2)  0  -(RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
        }

        /// Round bottom edge
        difference {
            cube {
                size (RAILTHICKNESS / 2)  RAILLENGTH  RAILTHICKNESS
                position (RAILTHICKNESS / 4)
            }
            cylinder {
                size RAILTHICKNESS  RAILLENGTH
            }

            position (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)  0  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
        }

        /// Top screw holes (for engine mounts)
        group {
            cylinder {
                size RAILMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  (ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }
            cylinder {
                size RAILMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  -(ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }

            position 0  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        group {
            cylinder {
                size RAILMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  (ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }
            cylinder {
                size RAILMOUNTSCREWHOLESIZE  RAILTHICKNESS
                orientation 0  0  0.5
                position 0  -(ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)  (RAILLEGSIZE / 2 - RAILTHICKNESS / 2)
            }

            position 0  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }

        /// Side screw holes (for supports)
        group {
            union {
                cylinder {
                    orientation 0.5
                    size 10  1000
                    position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLONGTITUDALHOLEOFFSET)
                }
                cylinder {
                    orientation 0.5
                    size 10  1000
                    position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLONGTITUDALHOLEOFFSET)
                }
                cylinder {
                    orientation 0.5
                    size 10  1000
                    position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLONGTITUDALHOLEOFFSET)
                }
                cylinder {
                    orientation 0.5
                    size 10  1000
                    position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLONGTITUDALHOLEOFFSET)
                }

                /// Move farther away from the wall
                position 0  0  -(RAILTHICKNESS / 2)
            }
        }

        /// Align top surface of the rail with the motor's center
        position 0  0  -(RAILLEGSIZE / 2)
    }
}

/// The electric motor
/// https://starboats.eu/electric-drives/e-tech-wg-inboard-engines/
define MOTOR {
    color 0  0  1

    union {
        /// Body
        cylinder {
            size MOTORBODYDIAMETER  MOTORBODYLENGTH
        }

        /// Shaft
        cylinder {
            color 100  100  100
            opacity 0.8

            size MOTORSHAFTDIAMETER  MOTORSHAFTLENGTH

            position 0  (MOTORBODYLENGTH / 2 + MOTORSHAFTLENGTH / 2)
        }

        /// Bump in the back
        cylinder {
            size 100  24
            position 0  -(MOTORBODYLENGTH / 2 + 24 / 2)
        }
    }
}

define MOTORBRACKETRAIL {
    color 100  100  100

    difference {
        /// Shape
        cube {
            size MOTORBRACKETLEGSIZE  1
        }
        cube {
            size (MOTORBRACKETLEGSIZE - MOTORBRACKETTHICKNESS)  1
            position (MOTORBRACKETTHICKNESS / 2)  0  -(MOTORBRACKETTHICKNESS / 2)
        }

        /// Round top edge
        difference {
            cube {
                size MOTORBRACKETTHICKNESS  1  MOTORBRACKETTHICKNESS / 2
                position 0  0  -(MOTORBRACKETTHICKNESS / 4)
            }
            cylinder {
                size MOTORBRACKETTHICKNESS
            }

            position -(MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)  0  -(MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)
        }

        /// Round bottom edge
        difference {
            cube {
                size (MOTORBRACKETTHICKNESS / 2)  1  MOTORBRACKETTHICKNESS
                position (MOTORBRACKETTHICKNESS / 4)
            }
            cylinder {
                size MOTORBRACKETTHICKNESS
            }

            position (MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)  0  (MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)
        }
    }
}

/// Stainless steel brackets bolted to both ends of the motor
define MOTORBRACKET {
    rotate 0.5

    union {
        /// Horizontal
        difference {
            MOTORBRACKETRAIL {
                size 1  MOTORBRACKETWIDTH
                orientation 1
            }

            /// Holes
            cylinder {
                orientation 0  0  0.5
                size 12  MOTORBRACKETTHICKNESS
                position -(MOTORBRACKETHOLESOFFSET)  (MOTORBRACKETHOLESDISTANCE / 2)  (MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)
            }
            cylinder {
                orientation 0  0  0.5
                size 12  MOTORBRACKETTHICKNESS
                position -(MOTORBRACKETHOLESOFFSET)  -(MOTORBRACKETHOLESDISTANCE / 2)  (MOTORBRACKETLEGSIZE / 2 - MOTORBRACKETTHICKNESS / 2)
            }
        }

        difference {
            union {
                /// Vertical (right)
                MOTORBRACKETRAIL {
                    size 1  MOTORBRACKETLEGLENGTH
                    orientation 0.5  0  0.5
                    position 0  (115 / 2 + MOTORBRACKETLEGSIZE / 2)  (MOTORBRACKETLEGLENGTH / 2 + MOTORBRACKETLEGSIZE / 2)
                }

                /// Vertical (left)
                MOTORBRACKETRAIL {
                    size 1  MOTORBRACKETLEGLENGTH
                    orientation 1  0  0.5
                    position 0  -(115 / 2 + MOTORBRACKETLEGSIZE / 2)  (MOTORBRACKETLEGLENGTH / 2 + MOTORBRACKETLEGSIZE / 2)
                }
            }

            /// Round cut-offs to fit into narrow bilge
            difference {
                cube {
                    size MOTORBRACKETLEGSIZE  MOTORBODYDIAMETER  MOTORBODYDIAMETER / 2
                    position 0  0  (MOTORBRACKETLEGLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + (MOTORBRACKETLEGLENGTH - MOTORBODYDIAMETER) / 2 + 14) + MOTORBODYDIAMETER / 4
                }
                cylinder {
                    size MOTORBODYDIAMETER  MOTORBRACKETLEGSIZE
                    orientation 0.5
                    position 0  0  (MOTORBRACKETLEGLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + (MOTORBRACKETLEGLENGTH - MOTORBODYDIAMETER) / 2 + 14)
                }
            }
        }

        /// Align bottom surface of the bracket with the motor's center
        position 0  0  -(MOTORBRACKETLEGSIZE / 2)
    }
}

define MOUNT {
    color 200  200  200
    opacity 0.9

    union {
        /// Core
        cube {
            size ENGINEMOUNTBASEPLATEWIDTH  80  ENGINEMOUNT
        }

        /// Base plate
        difference {
            cube {
                size ENGINEMOUNTBASEPLATEWIDTH  ENGINEMOUNTBASEPLATELENGTH  3
            }

            /// Screw holes
            cylinder {
                size ENGINEMOUNTSCREWHOLELENGTH  ENGINEMOUNTBASEPLATETHICKNESS
                orientation 0  0  0.5
                position 0  (ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)
            }
            cylinder {
                size ENGINEMOUNTSCREWHOLELENGTH  ENGINEMOUNTBASEPLATETHICKNESS
                orientation 0  0  0.5
                position 0  -(ENGINEMOUNTSCREWHOLEDISTANCE / 2 + ENGINEMOUNTSCREWHOLELENGTH / 2)
            }

            position 0  0  (ENGINEMOUNT / 2 - ENGINEMOUNTBASEPLATETHICKNESS / 2)
        }

        /// Central screw
        cylinder {
            opacity 0.8
            color 125  125  125
            orientation 0  0  0.5
            size 12  70
            position 0  0  -(ENGINEMOUNT / 2 + 70 / 2)
        }

        /// Nut
        cylinder {
            color 125  125  125
            size 31.35  10
            detail 6
            orientation 0  0  0.5
            position 0  0  -(ENGINEMOUNT / 2 + 10 / 2)
        }

        /// Align bottom surface of engine mounts with the motor's center
        position 0  0  -(ENGINEMOUNT / 2)
    }
}

define SUPPORT {
    color 0  100  0
    opacity 0.8

    union {
        cube {
            size (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT)
        }
        cube {
            size (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
        }
        cube {
            size (SUPPORTWIDTH)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
        }

        group {
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }

            position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
        }
        group {
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            sphere {
                size (SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }

            position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
        }
        group {
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTTHICKNESS - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  0  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
        }
        group {
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  (SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTWIDTH - SUPPORTANGLERADIUS * 2)
                position 0  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)  -(SUPPORTHEIGHT / 2 - SUPPORTANGLERADIUS)
            }
        }
        group {
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  (SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position (SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
            cylinder {
                orientation 0  0  0.5
                size (SUPPORTANGLERADIUS * 2)  (SUPPORTHEIGHT - SUPPORTANGLERADIUS * 2)
                position -(SUPPORTWIDTH / 2 - SUPPORTANGLERADIUS)  -(SUPPORTTHICKNESS / 2 - SUPPORTANGLERADIUS)
            }
        }

        /// Align top surface of supports with the motor's center
        position 0  0  (SUPPORTHEIGHT / 2)
    }
}

define HOLDER {
    color 0.75  0.75  0.5
    opacity 0.5

    difference {
        cube {
            size HOLDERWIDTH  HOLDERLENGTH  HOLDERHEIGHT
        }

        cube {
            size HOLDERWIDTH - HOLDERTHICKNESS  HOLDERLENGTH - HOLDERTHICKNESS  HOLDERHEIGHT
            position HOLDERTHICKNESS / 2  HOLDERTHICKNESS / 2
        }
    }
}

define MOTORWITHMOTORBRACKETS {
    MOTOR

    /// Brackets
    group {
        /// Front bracket
        MOTORBRACKET {
            position 0  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + GASKETTHICKNESS)
        }

        /// Back bracket
        MOTORBRACKET {
            orientation 1
            position 0  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + GASKETTHICKNESS)
        }

        /// Move up where it's supposed to be
        position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2)
    }
}

//////////////////////////////////////////////////////////////////////////////

MOTORWITHMOTORBRACKETS {
}

/// Rails
group {
    RAIL {
        orientation 1  1
        position (MOTORBRACKETHOLESDISTANCE / 2)
    }
    RAIL {
        orientation 0  1
        position -(MOTORBRACKETHOLESDISTANCE / 2)
    }

    position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET)
}

/// Engine mounts
group {
    MOUNT {
        position 150  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    MOUNT {
        position -150  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    MOUNT {
        position 150  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
    }
    MOUNT {
        position -150  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
    }

    position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET)
}

/// Supports
difference {
    union {
        SUPPORT {
            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        SUPPORT {
            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }

        position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILTHICKNESS - RAILVERTICALPOSITIONOFFSET - SUPPORTFIBERGLASSTHICKNESS)
    }

    /// Holes for securing rails using holders
    union {
        cylinder {
            size (SUPPORTHOLEDIAMETER)  (MOTORBODYLENGTH * 2)
            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + RAILLOADMIDDLE)
        }
        cylinder {
            size (SUPPORTHOLEDIAMETER)  (MOTORBODYLENGTH * 2)
            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + RAILLOADMIDDLE)
        }

        position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET - RAILTHICKNESS - RAILLOADMIDDLE)
    }
}

/// Holders
group {
    group {
        group {
            HOLDER {
                position 0 (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLENGTH / 2)
            }
            HOLDER {
                orientation 1  1
                position 0 (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLENGTH / 2)
            }

            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + HOLDERWIDTH / 2)
        }

        group {
            HOLDER {
                orientation 0  1
                position 0 (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLENGTH / 2)
            }
            HOLDER {
                orientation 1
                position 0 (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLENGTH / 2)
            }

            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + HOLDERWIDTH / 2)
        }
    }

    group {
        group {
            HOLDER {
                orientation 1  1
                position 0 -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLENGTH / 2)
            }
            HOLDER {
                position 0 -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLENGTH / 2)
            }

            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + HOLDERWIDTH / 2)
        }

        group {
            HOLDER {
                orientation 1
                position 0 -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS + SUPPORTTHICKNESS / 2 + SUPPORTFIBERGLASSTHICKNESS + HOLDERLENGTH / 2)
            }
            HOLDER {
                orientation 0  1
                position 0 -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS - SUPPORTTHICKNESS / 2 - SUPPORTFIBERGLASSTHICKNESS - HOLDERLENGTH / 2)
            }

            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + HOLDERWIDTH / 2)
        }
    }

    position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILVERTICALPOSITIONOFFSET - RAILTHICKNESS - RAILLOADMIDDLE)
}

/// Supports fiberglass
group {
    color 0.25  0.25  0.5
    opacity 0.75

    union {
        cube {
            size (SUPPORTWIDTH + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTTHICKNESS + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTHEIGHT + SUPPORTFIBERGLASSTHICKNESS * 2)
            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        cube {
            size (SUPPORTWIDTH + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTTHICKNESS + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTHEIGHT + SUPPORTFIBERGLASSTHICKNESS * 2)
            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  (MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        cube {
            size (SUPPORTWIDTH + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTTHICKNESS + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTHEIGHT + SUPPORTFIBERGLASSTHICKNESS * 2)
            position -(MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }
        cube {
            size (SUPPORTWIDTH + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTTHICKNESS + SUPPORTFIBERGLASSTHICKNESS * 2)  (SUPPORTHEIGHT + SUPPORTFIBERGLASSTHICKNESS * 2)
            position (MOTORBRACKETHOLESDISTANCE / 2 - RAILLEGSIZE / 2 + RAILTHICKNESS + SUPPORTWIDTH / 2 + SUPPORTFIBERGLASSTHICKNESS)  -(MOTORBODYLENGTH / 2 + MOTORBRACKETLEGSIZE / 2 + MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)
        }

        position 0  0  -(MOTORBRACKETDISTANCEFROMSHAFT + MOTORSHAFTDIAMETER / 2 - RAILTHICKNESS - RAILVERTICALPOSITIONOFFSET - (SUPPORTHEIGHT + SUPPORTFIBERGLASSTHICKNESS * 2) / 2)
    }

    size 0
}

/// Floor hatch
cube {
    opacity 0.25
    color 1  0  0
    size FLOORHATCHWIDTH  FLOORHATCHLENGTH  30
    position 0  0  -200
    size 0
}

//////////////////////////////////////////////////////////////////////////////

print "[RAIL] length: " (RAILLENGTH) "mm"
print "[RAIL] center of width: " (RAILLEGSIZE / 2) "mm"
print "[RAIL] distance from end to nearest engine mount screw hole's center: " ((RAILLENGTH - MOTORBODYLENGTH - MOTORBRACKETLEGSIZE - ENGINEMOUNTSCREWHOLEDISTANCE - ENGINEMOUNTSCREWHOLELENGTH) / 2 - (MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)) "mm"
print "[RAIL] distance from end to farthest engine mount screw hole's center: " ((RAILLENGTH - MOTORBODYLENGTH - MOTORBRACKETLEGSIZE + ENGINEMOUNTSCREWHOLEDISTANCE + ENGINEMOUNTSCREWHOLELENGTH) / 2 - (MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS)) "mm"
print "[RAIL] distance between innermost engine mount screw holes' centers: " (MOTORBODYLENGTH + MOTORBRACKETLEGSIZE - ENGINEMOUNTSCREWHOLEDISTANCE - ENGINEMOUNTSCREWHOLELENGTH + ((MOTORBRACKETHOLESOFFSET + GASKETTHICKNESS) * 2)) "mm"
print "[SUPPORTS] fiberglass layer thickness: " (SUPPORTFIBERGLASSTHICKNESS) "mm"
//print "[SUPPORTS] lateral distance between supports (without fiberglass): " (165 * 2 - SUPPORTTHICKNESS) "mm"
