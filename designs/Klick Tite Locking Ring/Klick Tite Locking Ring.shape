// ShapeScript document

/// Klick Tite Locking Ring

//////////////////////////////////////////////////////////////////////////////

detail 90

camera {
    position -28.2756  -25.5588  47.7696
    orientation 0.1527  0.0269  -0.1756
    width 618
    height 693
}

rotate 0  0  -0.5  /// CAD mode

//////////////////////////////////////////////////////////////////////////////

// define INCH_IN_MM                 25.399999619  /// mm
// define GAP                         0.2  /// mm

/// Connector
define CONNECTOR {  /// large diameter (mm), small diameter (mm), size (string)
    20  15.85  "#2"
}
define XXX  15.20
define CONNECTOR_RING_THICKNESS  2.50  /// mm
// 5.84
// 24.96
//////////////////////////////////////////////////////////////////////////////

define KLICK_TITE_LOCKING_RING {
    difference {
        /// Meat
        union {
            /// Rings + connection
            group {
                /// Large ring
                cylinder {
                    size ((XXX * 1.375) + 4.70 * 2)  (CONNECTOR_RING_THICKNESS)
                }

                /// Small ring
                cylinder {
                    size ((XXX + 0.5 * 2) + 4.5 * 2)  (CONNECTOR_RING_THICKNESS)
                    position -12
                }

                /// Connection between two outer rings
                cube {
                    size (XXX + 0.5 * 2 + 4.5 * 2) / 1.9  (CONNECTOR_RING_THICKNESS)  (XXX + 0.5 * 2 + 4.5 * 2)
                    position -(XXX / 3)
                }
            }

            /// Tab
            group {
                /// Bridge
                cube {
                    size (XXX * 1.175)  (CONNECTOR_RING_THICKNESS)
                    position -(XXX * 1.175)
                }

                /// Top
                group {
                    cube {
                        size (6)  (CONNECTOR_RING_THICKNESS * 4)  (36 - 6)
                        position -(XXX * 1.175)-XXX/2  (0)
                    }
                    cylinder {
                        size (6)  (CONNECTOR_RING_THICKNESS * 4)
                        position -(XXX * 1.175)-XXX/2  (0)  (36/2-6/2)
                    }
                    cylinder {
                        size (6)  (CONNECTOR_RING_THICKNESS * 4)
                        position -(XXX * 1.175)-XXX/2  (0)  -(36/2-6/2)
                    }

                    position 0  (CONNECTOR_RING_THICKNESS * 4 / 2 - CONNECTOR_RING_THICKNESS / 2)
                }

                /// Bottom
                group {
                    cube {
                        size (6)  (1.8)  (36 - 6)
                        position -(XXX * 1.175)-XXX/2
                    }
                    cylinder {
                        size (6)  (1.8)
                        position -(XXX * 1.175)-XXX/2  (0)  (36/2-6/2)
                    }
                    cylinder {
                        size (6)  (1.8)
                        position -(XXX * 1.175)-XXX/2  (0)  -(36/2-6/2)
                    }

                    cube {
                        size (XXX * 1.175)  (1.8)  (XXX * 1.175)
                        position -(XXX * 1.175)
                    }

                    position 0  -(CONNECTOR_RING_THICKNESS / 2 + 1.8 / 2)
                }
            }

            /// Lever
            intersection {
                difference {
                    cylinder {
                        size ((XXX * 1.375) + 4.70 * 2)  (3.4)
                    }
                    cylinder {
                        size ((XXX * 1.375) + 4.70 * 2 - 2.31 * 2)  (3.4)
                    }
                }

                cube {
                    size  20  (3.4)  (XXX + 0.5 * 2)
                    position 20
                }

                position 0  (3.4 / 2 + CONNECTOR_RING_THICKNESS / 2)
            }
        }

        /// Cutouts
        union {
            /// Holes + transition
            group {
                /// Large hole
                cylinder {
                    size (CONNECTOR[0])  (CONNECTOR_RING_THICKNESS)
                }

                /// Small hole
                cylinder {
                    size (XXX + 1)  (CONNECTOR_RING_THICKNESS)
                    position -12
                }

                /// Transition between holes
                cube {
                    size (XXX)  (CONNECTOR_RING_THICKNESS)
                    position -(XXX / 3)
                }
            }

            /// Side cutouts
            cube {
                size (XXX*2)  (CONNECTOR_RING_THICKNESS)  ((XXX * 1.375) - XXX)/2
                position 0  0  13.75
            }
            cube {
                size (XXX*2)  (CONNECTOR_RING_THICKNESS)  ((XXX * 1.375) - XXX)/2
                position 0  0  -13.75
            }

            /// Lock cutouts
            group {
                cube {
                    size (CONNECTOR[0] / 7.5)  (0.8)  6.85
                    position 10  -(CONNECTOR_RING_THICKNESS / 2 - 0.8 / 2)
                }

                cube {
                    size (20)  (1.6)  6.85
                    position 15  -(CONNECTOR_RING_THICKNESS / 2)
                    orientation -0.15
                }
            }

            /// Lever cutouts
            group {
                cylinder {
                    size (XXX + 6 * 2)  (CONNECTOR_RING_THICKNESS * 4 - CONNECTOR_RING_THICKNESS)
                    position -(12)  (CONNECTOR_RING_THICKNESS * 4 / 2)
                }

                cylinder {
                    size (XXX + XXX / 3 * 2)  (1.8)
                    position -(12)  -(CONNECTOR_RING_THICKNESS / 2 + 1.8 / 2)
                }
            }
        }
    }
}

//////////////////////////////////////////////////////////////////////////////

KLICK_TITE_LOCKING_RING {
    name "Klick Tite Locking Ring"
    color #f00a
}

//////////////////////////////////////////////////////////////////////////////

print "[MODEL] detail: " (detail)

print ""
print "[CONNECTOR] size: " (CONNECTOR[2])
print "[CONNECTOR] ring thickness: " CONNECTOR_RING_THICKNESS "mm"
