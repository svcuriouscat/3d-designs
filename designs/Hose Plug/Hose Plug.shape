// ShapeScript document

/// Hose Plug

detail 200
rotate 0  0  -0.5  /// CAD mode

////////////////////////////////////////////////////////////////

define INCH_IN_MM           25.399999619  /// mm
define GAP                   0.2  /// mm

/// Hose
define HOSE {
//    (INCH_IN_MM * 0.5)      "1/2\""     /// mm (0.5 inch)
//    (INCH_IN_MM * 0.75)     "3/4\""     /// mm (0.75 inch)
//    (INCH_IN_MM)            "1\""       /// mm (1.0 inch)
    (INCH_IN_MM * 1.5)      "1-1/2\""   /// mm (1.5 inch)
}
define HOSE_INT_DIAM       (HOSE[0])
define HOSE_NAME           (HOSE[1])

/// Plug
define PLUG_CAP_LEN        (HOSE_INT_DIAM / 2)  /// mm
define PLUG_CAP_DIAM       (HOSE_INT_DIAM * 2)
define PLUG_HOLE_DIAM      5
define PLUG_BARB_DIAM      (HOSE_INT_DIAM + 2.4)    /// mm
define PLUG_BARB_LEN       5.30     /// mm
define PLUG_STEM_LEN       (PLUG_BARB_LEN  * 6)     /// mm (needs to be long enough for 1–2 clamps)

////////////////////////////////////////////////////////////////

define PLUG_BARB {
    lathe path {
        point 0  PLUG_BARB_LEN
        point PLUG_BARB_DIAM/2  PLUG_BARB_LEN
        point HOSE_INT_DIAM/2  0
        point 0  0
    }
}

define PLUG_CAP {
    difference {
        cylinder {
            detail 6
            size PLUG_CAP_DIAM  PLUG_CAP_LEN * 1.5
            position 0  PLUG_CAP_LEN / 4
        }

        extrude {
            circle {
                size PLUG_CAP_DIAM - HOSE_INT_DIAM
            }
            along circle {
                orientation 0  0  0.5
                size (HOSE_INT_DIAM + (PLUG_CAP_DIAM - HOSE_INT_DIAM))
            }

            position 0  -((PLUG_CAP_DIAM - HOSE_INT_DIAM) / 2 - PLUG_CAP_LEN / 2)
        }
    }
}

define PLUG {
    difference {
        union {
            /// Core
            cylinder {
                size HOSE_INT_DIAM  PLUG_STEM_LEN
            }

            /// Barbs
            union {
                PLUG_BARB {
                    position 0  0
                }
                PLUG_BARB {
                    position 0  -(PLUG_BARB_LEN)
                }
                PLUG_BARB {
                    position 0  -(PLUG_BARB_LEN * 2)
                }
                PLUG_BARB {
                    position 0  -(PLUG_BARB_LEN * 3)
                }

                position 0  -(PLUG_STEM_LEN / 2 - (PLUG_BARB_LEN * 4 / 2) - PLUG_BARB_LEN)
            }

            /// Cap
            PLUG_CAP {
                position 0  (PLUG_STEM_LEN / 2 + (PLUG_CAP_LEN / 2))
            }
        }

        cylinder {
            orientation 0  0  0.5
            size PLUG_HOLE_DIAM  PLUG_BARB_DIAM
            position 0  PLUG_STEM_LEN / 2.5
        }
    }
}

////////////////////////////////////////////////////////////////

PLUG {
    name "Hose Plug — " (HOSE_NAME)
    color 75  75  75
}

////////////////////////////////////////////////////////////////

print "[MODEL] detail: " (detail)
print ""
print "[HOSE] variant: " (HOSE_NAME)
print "[HOSE] internal diameter: " (HOSE_INT_DIAM) "mm"

