// ShapeScript document

/// Hose Plug — 3/4"

detail 200
rotate 0  0  -0.5  /// CAD

/// Hose
define HOSEINTDIAM     14.64     /// mm
/// Cap
define CAPLEN          (HOSEINTDIAM / 2)  /// mm
define CAPDIAM         (HOSEINTDIAM * 2)
/// Misc
define BARBDIAM        16.28    /// mm
define BARBLEN         5.30     /// mm
define PLUGSTEMLENGTH  BARBLEN  * 6     /// mm (needs to be long enough for 1–2 clamps)
define HOLEDIAMETER    5

define BARB {
    lathe path {
        point 0  BARBLEN
        point BARBDIAM/2  BARBLEN
        point HOSEINTDIAM/2  0
        point 0  0
    }
}

define CAP {
    difference {
        cylinder {
            detail 6
            size CAPDIAM  CAPLEN * 1.5
            position 0  CAPLEN / 4
        }

        extrude {
            circle {
                size CAPDIAM - HOSEINTDIAM
            }
            along circle {
                orientation 0  0  0.5
                size (HOSEINTDIAM + (CAPDIAM - HOSEINTDIAM))
            }

            position 0  -((CAPDIAM - HOSEINTDIAM) / 2 - CAPLEN / 2)
        }
    }
}

define PLUG {
    difference {
        union {
            /// Core
            cylinder {
                size HOSEINTDIAM  PLUGSTEMLENGTH
            }

            /// Barbs
            union {
                BARB {
                    position 0  0
                }
                BARB {
                    position 0  -(BARBLEN)
                }
                BARB {
                    position 0  -(BARBLEN * 2)
                }
                BARB {
                    position 0  -(BARBLEN * 3)
                }

                position 0  -(PLUGSTEMLENGTH / 2 - (BARBLEN * 4 / 2) - BARBLEN)
            }

            /// Cap
            CAP {
                position 0  (PLUGSTEMLENGTH / 2 + (CAPLEN / 2))
            }
        }

        cylinder {
            orientation 0  0  0.5
            size HOLEDIAMETER  BARBDIAM
            position 0  PLUGSTEMLENGTH / 2.5
        }
    }
}

PLUG {
    color 75  75  75
}
