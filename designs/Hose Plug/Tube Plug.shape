// ShapeScript document

/// Tube Plug — 12mm
/// For polyethylene water pipes/tubes

detail 200
rotate 0  0  -0.5  /// CAD

/// Hose
define HOSE_INT_DIAM     8.8     /// mm
/// Cap
define CAPLEN          (HOSE_INT_DIAM / 2)  /// mm
define CAPDIAM         (HOSE_INT_DIAM * 2)
/// Misc
define BARBDIAM        (HOSE_INT_DIAM + 0.8)    /// mm
define BARBLEN         2.30     /// mm
define PLUGSTEMLENGTH  BARBLEN  * 6     /// mm (needs to be long enough for 1–2 clamps)
define HOLE_DIAMETER   2

define BARB {
    lathe path {
        point 0  BARBLEN
        point (BARBDIAM / 2)  BARBLEN
        point (HOSE_INT_DIAM / 2)  0
        point 0  0
    }
}

define CAP {
    difference {
        cylinder {
            detail 6
            size CAPDIAM  (CAPLEN * 1.5)
            position 0  (CAPLEN / 4)
        }

        extrude {
            circle {
                size (CAPDIAM - HOSE_INT_DIAM)
            }
            along circle {
                orientation 0  0  0.5
                size (HOSE_INT_DIAM + (CAPDIAM - HOSE_INT_DIAM))
            }

            position 0  -((CAPDIAM - HOSE_INT_DIAM) / 2 - CAPLEN / 2)
        }
    }
}

define PLUG {
    difference {
        union {
            /// Core
            cylinder {
                size HOSE_INT_DIAM  PLUGSTEMLENGTH
            }

            /// Barbs
            union {
                BARB {
                    position 0  0
                }
                BARB {
                    position 0  -(BARBLEN)
                }
                BARB {
                    position 0  -(BARBLEN * 2)
                }
                BARB {
                    position 0  -(BARBLEN * 3)
                }

                position 0  -(PLUGSTEMLENGTH / 2 - (BARBLEN * 4 / 2) - BARBLEN)
            }

            /// Cap
            CAP {
                position 0  (PLUGSTEMLENGTH / 2 + (CAPLEN / 2))
            }
        }

        cylinder {
            orientation 0  0  0.5
            size HOLE_DIAMETER  BARBDIAM
            position 0  (PLUGSTEMLENGTH / 2.5)
        }
    }
}

PLUG {
    color 75  75  75
}
